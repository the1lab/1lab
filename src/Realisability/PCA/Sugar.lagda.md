<!--
```agda
open import 1Lab.Prelude

open import Data.Partial.Total
open import Data.Partial.Base
open import Data.Fin.Base hiding (_<_ ; _â‰¤_)
open import Data.Nat.Base
open import Data.Vec.Base
open import Data.Irr

open import Realisability.PCA
```
-->

```agda
module Realisability.PCA.Sugar {â„“} (ğ”¸ : PCA â„“) where
```

<!--
```agda
private variable
  â„“' â„“'' : Level

open PCA ğ”¸ public
```
-->

# Sugar for programming in PCAs {defines="syntax-sugar-for-pcas"}

This module defines useful *meta*programs for writing [[values and
programs|values in a pca]] in a [[partial combinatory algebra]] $\bA$.
It is thus primarily interesting to the reader who cares about the
details of the formalisation, rather than its extensional mathematical
content.

We start by defining an overloaded version of the application operator
on $\bA$ which supports any argument that can be coerced to a partial
element of $\bA$--- so we can write things like $x \star y$ even when $x
: \bA$ is a literal element, or $y : \zap^+ \bA$ is a [[total partial
element]] of $\bA$.

```agda
_â‹†_
  : âˆ€ {X : Type â„“'} {Y : Type â„“''} â¦ƒ _ : To-part X âŒ ğ”¸ âŒŸ â¦„ â¦ƒ _ : To-part Y âŒ ğ”¸ âŒŸ â¦„
  â†’ X â†’ Y â†’ â†¯ âŒ ğ”¸ âŒŸ
f â‹† x = to-part f % to-part x where open To-part â¦ƒ ... â¦„
```

## Parametric higher-order abstract syntax

To conveniently use the abstraction elimination on $\bA$, we will define
a **parametric higher-order abstract syntax** for terms in $\bA$. PHOAS
is an approach to representing syntax with binding which parametrises
over a type $V$ of variables, and represents object-level binders with
meta-level binders.

```agda
data TermÊ° (V : Type) : Type â„“ where
  var   : V â†’ TermÊ° V
  const : â†¯âº âŒ ğ”¸ âŒŸ â†’ TermÊ° V
  app   : TermÊ° V â†’ TermÊ° V â†’ TermÊ° V
  lam   : (V â†’ TermÊ° V) â†’ TermÊ° V
```

We will primarily use terms where the type of variables is taken to be
the natural numbers, standing for de Bruijn *levels*. Since we can
perform case analysis on natural numbers, not every PHOAS `TermÊ°`{.Agda}
with natural-number values will represent an actual `Term`{.Agda} in the
language of $\bA$. We introduce a well-foundedness predicate `wf`{.Agda}
on PHOAS terms, given a context size $\Gamma$, which asserts that every
variable is actually in scope (and thus can be converted to a de Bruijn
*index* in $\Gamma$).

```agda
private
  wf : Nat â†’ TermÊ° Nat â†’ Type
  wf Î“ (var k)   = Î“ - suc k < Î“
  wf Î“ (const a) = âŠ¤
  wf Î“ (app f x) = wf Î“ f Ã— wf Î“ x
  wf Î“ (lam b)   = wf (suc Î“) (b Î“)
```

Note that `wf`{.Agda} is defined by recursion, rather than by induction,
so that all of its concrete instances can be filled in by instance
search. We can convert a `wf`{.Agda} PHOAS term in $\Gamma$ to a de
Bruijn term in $\Gamma$. We use levels rather than indices in the PHOAS
representation so that the case for abstractions can call the meta-level
abstraction with the length of the context.

```agda
  from-wf : âˆ€ {Î“} (t : TermÊ° Nat) â†’ wf Î“ t â†’ Term âŒ ğ”¸ âŒŸ Î“
  from-wf {Î“} (var x) w       = var (fin (Î“ - suc x) â¦ƒ forget w â¦„)
  from-wf (const x)   w       = const x
  from-wf (app f x) (wf , wx) = app (from-wf f wf) (from-wf x wx)
  from-wf {Î“} (lam f) w       = abs (from-wf (f Î“) w)
```

Finally, we introduce another recursive predicate to be filled in by
instance search indicating whether a term always denotes--- these are
the inclusions of elements from $\bA$ and top-level abstractions.

```agda
  always-denotes : âˆ€ {V} â†’ TermÊ° V â†’ Type
  always-denotes (var x)   = âŠ¥
  always-denotes (const x) = âŠ¤
  always-denotes (app f x) = âŠ¥
  always-denotes (lam x)   = âŠ¤
```

To use PHOAS terms, we introduce notations for evaluating an arbitrary
expression *and* a term which always denotes, producing a partial or
total-partial element of $\bA$ respectively. Note the *parametricity* in
the argument: to prevent us from writing 'exotic' values of
`TermÊ°`{.Agda}, we must work against an arbitrary choice of variable
type. If Agda had internalised parametricity, this would be enough to
prove that the arguments to `expr_`{.Agda} and `val_`{.Agda} must be
well-formed; since we do not have parametricity we instead ask instance
search to fill in the well-formedness (and definedness) assumptions
instead.

```agda
expr_ : (t : âˆ€ {V} â†’ TermÊ° V) â¦ƒ _ : wf 0 t â¦„ â†’ â†¯ âŒ ğ”¸ âŒŸ
expr_ t â¦ƒ i â¦„ = eval {n = 0} (from-wf t i) []

val_
  : (t : âˆ€ {V} â†’ TermÊ° V) â¦ƒ _ : wf 0 t â¦„
  â†’ â¦ƒ _ : always-denotes {Nat} t â¦„ â†’ â†¯âº âŒ ğ”¸ âŒŸ
val_ t â¦ƒ i â¦„ =
  record
    { fst = eval {n = 0} (from-wf t i) []
    ; snd = d t
    }
  where abstract
  d : (t : TermÊ° Nat) â¦ƒ i : wf 0 t â¦„ â¦ƒ _ : always-denotes t â¦„ â†’ âŒ eval {n = 0} (from-wf t i) [] âŒŸ
  d (const x) = x .snd
  d (lam x) = absâ†“ (from-wf (x 0) _) []
```

Finally, we introduce a notation class `To-term`{.Agda} to overload the
construction of applications and abstractions in PHOAS terms.

```agda
record To-term {â„“'} (V : Type) (X : Type â„“') : Type (â„“ âŠ” â„“') where
  field to : X â†’ TermÊ° V

instance
  var-to-term : âˆ€ {V} â†’ To-term V V
  var-to-term = record { to = var }

  const-to-term' : âˆ€ {V} â†’ To-term V âŒ ğ”¸ âŒŸ
  const-to-term' = record { to = Î» x â†’ const (pure x , tt) }

  const-to-term : âˆ€ {V} â†’ To-term V (â†¯âº âŒ ğ”¸ âŒŸ)
  const-to-term = record { to = const }

  term-to-term : âˆ€ {V} â†’ To-term V (TermÊ° V)
  term-to-term = record { to = Î» x â†’ x }

_`Â·_
  : âˆ€ {â„“' â„“''} {V : Type} {A : Type â„“'} {B : Type â„“''}
  â†’ â¦ƒ _ : To-term V A â¦„ â¦ƒ _ : To-term V B â¦„
  â†’ A â†’ B â†’ TermÊ° V
f `Â· x = app (to f) (to x) where open To-term â¦ƒ ... â¦„

lam-syntax : âˆ€ {â„“} {V : Type} {A : Type â„“} â¦ƒ _ : To-term V A â¦„ â†’ (V â†’ A) â†’ TermÊ° V
lam-syntax f = lam Î» x â†’ to (f x) where open To-term â¦ƒ ... â¦„

syntax lam-syntax (Î» x â†’ e) = âŸ¨ x âŸ© e

infixl 25 _`Â·_
infixl 35 _â‹†_
```
