---
talk: yes
slide-level: 2
---

# Cubical types for the working formaliser

:::{#author}
AmÃ©lia Liao
:::

<!--
```agda
{-# OPTIONS --allow-unsolved-metas #-}
open import 1Lab.Path.Reasoning
open import 1Lab.Prelude hiding (funext ; Extensional ; ext ; uncurry ; id ; _âˆ˜_ ; _==_)

open import Algebra.Group.Ab.Tensor
open import Algebra.Group.Cat.Base hiding (Displayed)
open import Algebra.Group.Ab
open import Algebra.Group

open import Data.Nat.Base hiding (_==_)
open import Data.Int.Base
open import Data.Sum

open import Cat.Base

open import Algebra.Group.Concrete.Abelian

open import Homotopy.Space.Circle

import Cat.Reasoning as Cat

module Talks.Topos2024 where

private variable
  â„“ â„“r : Level
  A B C : Type â„“
  w x y z : A
  n k : Nat
  P : A â†’ Type â„“
  Q : (x : A) â†’ P x â†’ Type â„“

open is-iso

module _ where
```
-->

## The 1Lab

Open source combination wiki (Ã  la nLab) + Agda library.

. . .

* Reimplementations of everything we need, e.g.

```agda
  open import 1Lab.Path
```

contains basic results about identity.

. . .

* Lots of category theory (see [Elephant](Elephant.html),
  [Borceux](Borceux.html) index pages)

* Very basic results in algebra + synthetic homotopy theory (see [HoTT ch. 8](HoTT.html#chapter-8-homotopy-theory))

. . .

This talk is a module in the 1Lab!

## The 1Lab as a library

* Constructive but *im*predicative (propositional resizing); homotopical
  features used freely

* Type classes for automation, but only of *properties*; equipping a
  type with *structure* (e.g. algebraic) is always explicit

* Playground for new Agda features, e.g. `opaque`{.kw}, `OVERLAPPING`{.kw} instances

## This talk

<!--
```agda
_ = uaÎ²
```
-->

* HoTT simplifies Martin-LÃ¶f type theory by giving universes a nice
  universal property ([[univalence]])

* Cubical type theory *complicates* type theory to make this compute
  (`uaÎ²`{.Agda})

Lots of work has gone into usability of traditional proof assistants;
but what about *higher dimensional* proof assistants?

. . .

We'll talk about:

* Extensional equality
* Structure identity

# Extensional equality

When are functions $f, g : A \to B$ identical?

:::incremental
* Expected answer: whenever $f(x) \is g(x)$ for all $x$;
* Actual answer: ğŸ¤·â€â™€ï¸!

  Extensionality *independent of* MLTT. E.g. [antifunext](https://github.com/AndrasKovacs/antifunext)
:::

. . .

Our solution: [the interval](1Lab.Path.html), [[paths]].

---

<!--
```
_ = I
_ = i0
_ = i1
```
-->

A type `I`{.Agda}, with *endpoints* `i0`{.Agda}, `i1`{.Agda}, which
(co)represents identifications.

An identification $p : x \is_A y$ is an element $i : \bI, p(i) : A$ that
satisfies $p(i0) = x$ and $p(i1) = y$.

In Agda: path lambdas and path application.

```agda
_ : {A : Type} (x : A) â†’ x â‰¡ x
_ = Î» x i â†’ x
```

---

<!--
```agda
module _ where private
```
-->

```agda
  funext : (f g : A â†’ B) (h : âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g
  funext f g h = {!   !}
  -- Goal: f â‰¡ g
  -- Context:
  --   f, g : A â†’ B
  --   h : (x : A) â†’ f x â‰¡ g x
```

---

<!--
```agda
module _ where private
```
-->

We can introduce a path with another abstraction:

```agda
  funext : (f g : A â†’ B) (h : âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g
  funext f g h i = {!   !}
  -- Goal: A â†’ B
  -- Context: [as before], i : I
```

Not any `A â†’ B` will do!

```agda
  -- Boundary:
  --   i = i0 âŠ¢ Î» x â†’ f x
  --   i = i1 âŠ¢ Î» x â†’ g x
```

---

<!--
```agda
module _ where private
```
-->

Goal is a function type, so we can abstract *another* argument:

```agda
  funext : (f g : A â†’ B) (h : âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g
  funext f g h i x = {!   !}
  -- Goal: B
  -- Context: [as before], x : A
  -- Boundary:
  --   i = i0 âŠ¢ f x
  --   i = i1 âŠ¢ g x
```

---

<!--
```agda
module _ where private
```
-->

Because paths restrict to their endpoints, we're done!

```agda
  funext : {f g : A â†’ B} (h : âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g
  funext {f} {g} h i x = h x i
```

. . .

<br>

```agda
  _ : {f : A â†’ B} â†’ funext {f = f} (Î» x â†’ refl) â‰¡ refl
  _ = refl

  _ : {f g : A â†’ B} {h : âˆ€ x â†’ f x â‰¡ g x}
    â†’ funext (Î» x â†’ sym (h x)) â‰¡ sym (funext h)
  _ = refl

  _ : {f g h : A â†’ B} {Î± : âˆ€ x â†’ f x â‰¡ g x} {Î² : âˆ€ x â†’ g x â‰¡ h x}
    â†’ funext (Î» x â†’ Î± x âˆ™ Î² x) â‰¡ funext Î± âˆ™ funext Î²
  _ = refl
```

---

Introducing path abstractions does a number on inference:

<!--
```agda
module _ where private
```
-->

```agda
  what : ((x y : A) â†’ x â‰¡ y) â†’ (x y : A) â†’ x â‰¡ y
  what h x y i = h _ _ i
```

Agda generates constraints:

<pre class=Agda>
  <span class="UnsolvedMeta">_x</span> h x y (i = <span class="InductiveConstructor">i0</span>) = x
  <span class="UnsolvedMeta">_y</span> h x y (i = <span class="InductiveConstructor">i1</span>) = y
</pre>

Neither of these is fully determined! <br>
Both metas go unsolved.

. . .

Need a nice *interface* for extensional equality.

# A nice interface

Ideally: the type $x \is_A y$ *computes* to something simpler based on $A$.

* Observational type theory with strict propositions...

    + Pro: computed identity type is basically always right;
    + Con: doesn't scale to homotopy.

* Higher observational type theory...

    + Pro: scales to homotopy;
    + Con: computed identity system will in many cases be overcomplicated.

Key example: group homomorphisms $f, g : G \to H$. These are pairs, so
$f \is g$ computes to a pair type. <br>
But the second component --- the proof that $f$ is a homomorphism --- is
irrelevant!

. . .

Without a sort of strict propositions, the system can't "see" this. <br>
So "primitive higher-OTT" will still need a helper, to fill in the
trivial second component. <br>

---

The idea is old: type classes! Essentially:

<!--
```agda
open 1Lab.Prelude using (funext)

module _ where private
```
-->

```agda
  record Extensional (A : Type) : Type (lsuc lzero) where
    field
      _â‰ˆ_     : A â†’ A â†’ Type
      to-path : âˆ€ {x y : A} â†’ x â‰ˆ y â†’ x â‰¡ y
```

<!--
```agda
  open Extensional â¦ƒ ... â¦„ renaming (to-path to ext) using ()
  open Extensional
```
-->

. . .

We have an instance for each type former we want to compute:

```agda
  instance
    ext-fun : â¦ƒ Extensional B â¦„ â†’ Extensional (A â†’ B)
    ext-fun â¦ƒ e â¦„ = record
      { _â‰ˆ_     = Î» f g â†’ âˆ€ x â†’ e ._â‰ˆ_ (f x) (g x)
      ; to-path = Î» h â†’ funext Î» x â†’ e .to-path (h x)
      }
```

. . .

And an *overlappable* instance for the base case:

```agda
    ext-base : Extensional A
    ext-base = record { to-path = Î» x â†’ x }
    {-# OVERLAPPABLE ext-base #-}
```

---

We can test that this works by asking Agda to check `ext`{.Agda} at
various types:

```agda
  _ : {A B : Type} {f g : A â†’ B} â†’ (âˆ€ x â†’ f x â‰¡ g x) â†’ f â‰¡ g
  _ = ext

  _ : {A B C : Type} {f g : A â†’ B â†’ C} â†’ (âˆ€ x y â†’ f x y â‰¡ g x y) â†’ f â‰¡ g
  _ = ext

  _ : {A : Type} {x y : A} â†’ x â‰¡ y â†’ x â‰¡ y
  _ = ext
```

. . .

A benefit: type class solving *isn't a real function*. Can be unstable
under substitution!

```agda
  instance
    ext-uncurry : â¦ƒ Extensional (A â†’ B â†’ C) â¦„ â†’ Extensional (A Ã— B â†’ C)
    ext-uncurry â¦ƒ e â¦„ = record
      { _â‰ˆ_     = Î» f g â†’ e ._â‰ˆ_ (curry f) (curry g)
      ; to-path = Î» h i (x , y) â†’ e .to-path h i x y
      }
    {-# OVERLAPPING ext-uncurry #-}
```

The resulting relation has three arguments, rather than two:

```agda
  _ : {A B C D : Type} {f g : A Ã— B â†’ C â†’ D}
    â†’ (âˆ€ a b c â†’ f (a , b) c â‰¡ g (a , b) c)
    â†’ f â‰¡ g
  _ = ext
```

---

<!--
```agda
open 1Lab.Prelude using (ext)

module _ where private
```
-->

The real implementation handles maps of algebraic structures, e.g.
groups,

```agda
  _ : {G H : Group lzero} {f g : Groups.Hom G H}
    â†’ ((x : âŒ G âŒŸ) â†’ f # x â‰¡ g # x) â†’ f â‰¡ g
  _ = ext
```

maps *from* certain higher inductive types, e.g. generic set-quotients,
or abelian group homomorphisms from a tensor product,

```agda
  _ : {A B C : Abelian-group lzero} {f g : Ab.Hom (A âŠ— B) C}
    â†’ ((x : âŒ A âŒŸ) (y : âŒ B âŒŸ) â†’ f # (x , y) â‰¡ g # (x , y))
    â†’ f â‰¡ g
  _ = ext
```

and also: natural transformations, maps in slice categories, in comma
categories, in categories of elements, in wide and full subcategories,
in categories of monad algebras, type-theoretic equivalences and
embeddings, monotone maps, &c., &c.

# Structure identity

Same setup: when are types $A, B : \ty$ identical? <br>
Ideal answer: when they are *indistinguishable*. <br>
What distinguishes types?

Take e.g. $\bN \times \mathtt{String}$ vs. $\mathtt{String} \times \bN$:

::: incremental

* In base MLTT (or even e.g. Lean): no proof that they're distinct

* ... because if you give me any property that holds of one, I can
  modify your proof to show it holds of the other (by hand)

* ... so these types should be identical!

:::

. . .

The actual answer: ğŸ¤·â€â™€ï¸ <br>
Univalence says they're the same; (set-level) OTT says they're
different; everyone else is undecided

---

Univalence says $(A \is B)$ equivalent to $(A \simeq B)$:

* Just as much control over $p : A \is B$ as we would have over $f : A \simeq B$;

  + E.g. *exactly* two inhabitants $\ua(\id)$, $\ua(\operatorname{not})$ in $\{0, 1\} \is \{0, 1\}$

* Not often interested in arbitrary types. More often: types with *structure*.

  Example: monoids.

. . .

A monoid $M$ is a tuple $(M, *, 1, ...)$ consisting of

* The *stuff* drawn from $M$;
* The *structure* of the multiplication and identity;
* The *property*: proofs of identity on the left & right and associativity;

When are two monoids the same? <br>

---

Let's make it a bit more formal: we define the property, parametrised
over the stuff and the structure;

```agda
is-monoid : (M : Type) â†’ (M â†’ M â†’ M) â†’ M â†’ Type
is-monoid M m u =
  is-set M            -- required so is-monoid is a property
  Ã— (âˆ€ x â†’ m u x â‰¡ x) -- left identity
  Ã— (âˆ€ x â†’ m x u â‰¡ x) -- right identity
  Ã— (âˆ€ x y z â†’ m x (m y z) â‰¡ m (m x y) z) -- associativity
```

We'll need that it's an actual property:

```agda
is-monoid-is-prop : âˆ€ M m u â†’ is-prop (is-monoid M m u)
```

<!--
```agda
is-monoid-is-prop _ _ _ = Î£-is-hlevel 1 (is-hlevel-is-prop 2) Î» mset â†’
  let instance _ = hlevel-instance {n = 2} mset in hlevel 1
```
-->

Then we sum it all:

```agda
Monoid : Typeâ‚
Monoid =
  Î£[ M âˆˆ Type ]                   -- stuff
  Î£[ m âˆˆ (M â†’ M â†’ M) ] Î£[ u âˆˆ M ] -- structure
    (is-monoid M m u)             -- property
```

Completely formally, a `Monoid`{.Agda} has 7 fields, but we've shown
that 4 don't matter.

---

Then we can compute the identity type: If we have monoids $M = (M_0, m, u,
\alpha)$ and $N = (N_0, n, v, \beta)$, what is $M \is N$?

```agda
module _ (M@(Mâ‚€ , m , u , Î±) N@(Nâ‚€ , n , v , Î²) : Monoid) where
```

<br>

. . .

Step 1: an identity of tuples is a tuple of identities, and identity in
`is-monoid`{.Agda} is trivial;

```agda
  Stepâ‚ : Typeâ‚
  Stepâ‚ = Î£[ p âˆˆ Mâ‚€ â‰¡ Nâ‚€ ]
    ( PathP (Î» i â†’ p i â†’ p i â†’ p i) m n
    Ã— PathP (Î» i â†’ p i) u v
    )

  stepâ‚ : Stepâ‚ â†’ M â‰¡ N
  stepâ‚ (p , q , r) i = p i , q i , r i ,
    is-propâ†’pathp (Î» i â†’ is-monoid-is-prop (p i) (q i) (r i)) Î± Î² i
```

---

Step 2: we can simplify the `PathP`{.Agda}s to talk about transport instead:

```agda
  Stepâ‚‚ : Typeâ‚
  Stepâ‚‚ = Î£[ p âˆˆ Mâ‚€ â‰¡ Nâ‚€ ]
    ( (âˆ€ x y â†’ transport p (m x y) â‰¡ n (transport p x) (transport p y))
    Ã— transport p u â‰¡ v
    )

  stepâ‚‚ : Stepâ‚‚ â†’ Stepâ‚
  stepâ‚‚ (p , q , r) = p , q' , to-pathp r where
    -- q' actually pretty complicated...
    q' = funext-dep Î» Î± â†’ funext-dep Î» Î² â†’
      to-pathp (q _ _ âˆ™ apâ‚‚ n (from-pathp Î±) (from-pathp Î²))
```

---

Step 3: we know what identity of types is!

```agda
  Stepâ‚ƒ : Type
  Stepâ‚ƒ = Î£[ p âˆˆ Mâ‚€ â‰ƒ Nâ‚€ ] -- equivalence!
    ( (âˆ€ x y â†’ p .fst (m x y) â‰¡ n (p .fst x) (p .fst y))
    Ã— p .fst u â‰¡ v
    )
```

Takes a bit of massaging...

```agda
  stepâ‚ƒ : Stepâ‚ƒ â†’ Stepâ‚‚
  stepâ‚ƒ (p , q , r) = ua p , q' , r' where
    r' = transport (ua p) u â‰¡âŸ¨ uaÎ² p u âŸ©
         p .fst u           â‰¡âŸ¨ r âŸ©
         v                  âˆ

    q' : âˆ€ x y â†’ _
    q' x y =
      transport (ua p) (m x y)                    â‰¡âŸ¨ uaÎ² p (m x y) âŸ©
      p .fst (m x y)                              â‰¡âŸ¨ q x y âŸ©
      n (p .fst x) (p .fst y)                     â‰¡âŸ¨ apâ‚‚ n (sym (uaÎ² p x)) (sym (uaÎ² p y)) âŸ©
      n (transport (ua p) x) (transport (ua p) y) âˆ
```

The conclusion: identity in the type `Monoid`{.Agda} is *exactlyÂ¹*
monoid isomorphism!

# Designing for structure identity

While it's great that the theory works, the proofs are pretty annoying. <br>
But they're very mechanical --- incremental! <br>

Our solution: put together categories of structures *in layers*. <br>
Displayed categories!

A category over $\cC$ is an alternative presentation of the data of a
category $\cD$ and a functor $\pi : \cD \to \cC$; just more "native" to
type theory.

<!--
```agda
private module _ {o â„“ : Level} (C : Precategory o â„“) where
  open Precategory C
```
-->

---

The idea: turn fibers into families.

```agda
  record Displayed o' â„“' : Type (o âŠ” â„“ âŠ” lsuc (o' âŠ” â„“')) where
    field
      Ob[_]  : âŒ C âŒŸ â†’ Type o'
      Hom[_] : (f : Hom x y) â†’ Ob[ x ] â†’ Ob[ y ] â†’ Type â„“'
```

. . .

Stuff over stuff, structure over structure:

```agda
      id'  : {x' : Ob[ x ]} â†’ Hom[ id ] x' x'
      _âˆ˜'_
        : {x' : Ob[ x ]} {y' : Ob[ y ]} {z' : Ob[ z ]}
        â†’ {f : Hom y z} (f' : Hom[ f ] y' z')
        â†’ {g : Hom x y} (g' : Hom[ g ] x' y')
        â†’ Hom[ f âˆ˜ g ] x' z'
```

... also need property over property; check the formalisation.

---

If we start with a functor, then $\operatorname{Ob}[x]$ is the type
$\sum_{y : \cD} \pi(y) \cong x$; the "fibre" of $\pi$ over $x$.

<!--
```agda
private module _ {o â„“ o' â„“' : Level} {C : Precategory o â„“} {D : Precategory o' â„“'} (Ï€ : Functor D C) where private
  private
    module C = Cat C
    module D = Cat D
    module Ï€ = Functor Ï€
    open C using (_âˆ˜_ ; id ; _â‰…_)

    open C._â‰…_
  open Displayed
```
-->

```agda
  Street : Displayed C _ _
  Street .Ob[_] x = Î£[ y âˆˆ D ] (Ï€.â‚€ y â‰… x)
```

The maps over are more complicated:

```agda
  Street .Hom[_] f (x' , i) (y' , j) = Î£[ g âˆˆ D.Hom x' y' ]
    (Ï€.â‚ g â‰¡ j .from âˆ˜ f âˆ˜ i .to)
```

Accordingly, the structure is pretty annoying:

```agda
  Street .id' {x} {x' , i} = record { snd =
    Ï€.â‚ D.id               â‰¡âŸ¨ Ï€.F-id âŸ©
    id                     â‰¡Ë˜âŸ¨ i .invr âŸ©
    i .from âˆ˜ i .to        â‰¡Ë˜âŸ¨ apâ‚‚ _âˆ˜_ refl (C.idl _) âŸ©
    i .from âˆ˜ id C.âˆ˜ i .to âˆ }
  Street ._âˆ˜'_ = _ -- even worse!
```

---

If we start with a displayed category $\cE \liesover \cC$, we can
put together a category with objects $\sum_{x : \cC}
\operatorname{Ob}[x]$ --- the [[total category]] $\int \cE$.

Similarly, each $x : \cC$ gives a category $\cE^*(x)$ with objects
$\operatorname{Ob}[x]$ and maps $\operatorname{Hom}[f](-,-)$

Properties of the functor $\pi : \int \cE \to \cC$ map very nicely to
properties of $\cE$!

+--------------------+------------------------------------------------------------------------------+
| $\pi$ is...        | $\cE$ satisfies...                                                           |
+====================+==============================================================================+
| [[faithful]]       | each $\operatorname{Hom}[-](-,-)$ is a [[proposition]]                       |
+--------------------+------------------------------------------------------------------------------+
| [[full]]           | each $\operatorname{Hom}[-](-,-)$ is [[inhabited\|propositional truncation]] |
+--------------------+------------------------------------------------------------------------------+
| [[fully faithful]] | each $\operatorname{Hom}[-](-,-)$ is [[contractible]]                        |
+--------------------+------------------------------------------------------------------------------+
| [[amnestic]]       | each $\cE^*(x)$ univalent, and so is $\cC$                                   |
+--------------------+------------------------------------------------------------------------------+

That last row is important!

---

We can present a concrete, univalent category as a displayed category.

* Concrete: the base is $\Sets$ and each $\operatorname{Hom}[-](-,-)$ is a proposition.
* Univalent: each $\cE^*(x)$ is a partial order.

. . .

These data amount to:

1. A type family of *structures* $F : \ty \to \ty$;
2. A proposition $\operatorname{Hom}[f](S, T)$, given $f : A \to B$, $S : F(A)$, $T : F(B)$;<br>
  Type encoding "$f$ is a structure-preserving map from $S$ to $T$";
3. Proofs that (2) contains the identities and is closed under composition;
4. For univalence: a proof that if $\id$ is a structure preserving map
   $S \to T$ (and also $T \to S$), then $S = T$.

# Concrete example

We start by defining the property, parametrised by the structure:

```agda
record is-semigroup {A : Type â„“} (_â‹†_ : A â†’ A â†’ A) : Type â„“ where
  field
    has-is-set : is-set A
    assoc      : âˆ€ {x y z} â†’ x â‹† (y â‹† z) â‰¡ (x â‹† y) â‹† z
```

. . .

We can derive that this *is* a property pretty mechanically:

```agda
module _ where
  private unquoteDecl eqv = declare-record-iso eqv (quote is-semigroup)

  is-semigroup-is-prop : {A : Type â„“} {s : A â†’ A â†’ A} â†’ is-prop (is-semigroup s)
  is-semigroup-is-prop = Isoâ†’is-hlevel 1 eqv $ Î£-is-hlevel 1 (hlevel 1) Î» x â†’
    let instance _ = hlevel-instance {n = 2} x in hlevel 1
```

---

Then we define the structure:

```agda
record Semigroup-on (A : Type â„“) : Type â„“ where
  field
    _â‹†_              : A â†’ A â†’ A
    has-is-semigroup : is-semigroup _â‹†_
  open is-semigroup has-is-semigroup public
```

... and what it means for functions to preserve it:

```agda
record is-semigroup-hom (f : A â†’ B) (S : Semigroup-on A) (T : Semigroup-on B)
  : Type (level-of A âŠ” level-of B) where

  private
    module S = Semigroup-on S
    module T = Semigroup-on T

  field
    pres-â‹† : âˆ€ x y â†’ f (x S.â‹† y) â‰¡ f x T.â‹† f y
```

<!--
```agda
private
  unquoteDecl eqv = declare-record-iso eqv (quote Semigroup-on)
  unquoteDecl eqv' = declare-record-iso eqv' (quote is-semigroup-hom)

instance
  H-Level-is-semigroup : âˆ€ {n} {s : A â†’ A â†’ A} â†’ H-Level (is-semigroup s) (suc n)
  H-Level-is-semigroup = prop-instance is-semigroup-is-prop

  H-Level-is-semigroup-hom
    : âˆ€ {n} {f : A â†’ B} {S : Semigroup-on A} {T : Semigroup-on B}
    â†’ H-Level (is-semigroup-hom f S T) (suc n)
  H-Level-is-semigroup-hom {T = T} = prop-instance (Isoâ†’is-hlevel 1 eqv' (hlevel 1))
    where instance _ = hlevel-instance {n = 2} (T .Semigroup-on.has-is-set)

open is-semigroup-hom
```
-->

---

We can then fill in the four bullet points. $F$ is
`Semigroup-on`{.Agda}, $\operatorname{Hom}[-](-,-)$ is
`is-semigroup-hom`{.Agda},

```agda
Semigroup-structure : âˆ€ {â„“} â†’ Thin-structure â„“ Semigroup-on
Semigroup-structure .is-hom f S T = el! (is-semigroup-hom f S T)
```

... identities and composites are respected ...

```agda
Semigroup-structure .id-is-hom .pres-â‹† x y = refl
Semigroup-structure .âˆ˜-is-hom f g Î± Î² .pres-â‹† x y =
  ap f (Î² .pres-â‹† x y) âˆ™ Î± .pres-â‹† _ _
```

and, finally, the univalence condition:

```agda
Semigroup-structure .id-hom-unique p q = Iso.injective eqv $ Î£-prop-path! $
  ext Î» a b â†’ p .pres-â‹† a b
```
