<!--
```agda
open import 1Lab.Reflection

open import Cat.Diagram.Product
open import Cat.Prelude

open import Data.List

import Cat.Reasoning
```
-->

```agda
module Cat.Diagram.Product.Solver where
```

# A solver for categories with binary products

Much like the [category solver], this module is split into two halves.
The first implements an algorithm for normalizing expressions in the
language of a category with binary products. The latter half consists
of the usual reflection hacks required to transform Agda expressions
into our internal expression type.

[category solver]: Cat.Solver.html

```agda
module NbE {o â„“} (ğ’ : Precategory o â„“) (cartesian : âˆ€ A B â†’ Product ğ’ A B) where
  open Cat.Reasoning ğ’
  open Binary-products ğ’ cartesian
```

## Expressions

We begin by defining an expression type for a category with binary
products. Mathematically, this /almost/ corresponds to the free
category with binary products over a quiver, but we are working
with un-quotiented syntax.

```agda
  data â€¶Obâ€¶ : Type (o âŠ” â„“) where
    _â€¶âŠ—â€¶_ : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ â€¶Obâ€¶
    â€¶_â€¶   : Ob â†’ â€¶Obâ€¶

  âŸ¦_âŸ§â‚’ : â€¶Obâ€¶ â†’ Ob
  âŸ¦ X â€¶âŠ—â€¶ Y âŸ§â‚’ =  âŸ¦ X âŸ§â‚’ âŠ—â‚€ âŸ¦ Y âŸ§â‚’
  âŸ¦ â€¶ X â€¶ âŸ§â‚’ = X

  data Expr : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” â„“) where
    â€¶idâ€¶    : âˆ€ {X} â†’ Expr X X
    _â€¶âˆ˜â€¶_   : âˆ€ {X Y Z} â†’ Expr Y Z â†’ Expr X Y â†’ Expr X Z
    â€¶Ï€â‚â€¶    : âˆ€ {X Y} â†’ Expr (X â€¶âŠ—â€¶ Y) X
    â€¶Ï€â‚‚â€¶    : âˆ€ {X Y} â†’ Expr (X â€¶âŠ—â€¶ Y) Y
    â€¶âŸ¨_,_âŸ©â€¶ : âˆ€ {X Y Z} â†’ Expr X Y â†’ Expr X Z â†’ Expr X (Y â€¶âŠ—â€¶ Z)
    â€¶_â€¶     : âˆ€ {X Y} â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’ â†’ Expr X Y
```

Note that we also define a syntax for products of objects
in this free category, even though the ambient category
`ğ’`{.Agda} already has binary products. The reason for this is two-fold.
The first, more mundane reason is that the unifier will get very confused
if we don't do this. The second reason is much more mathematically
interesting, as it pertains to our choice of normalization algorithm.

Much like the aforementioned [category solver], we are going to be
using a variant of *Normalization By Evaluation* (NbE for short).
This class of normalization algorithms operates by constructing a
domain of "values", which are meant to denote the semantics of some
expression. Normalization then occurs in 2 phases: an "evaluation"
phase where we transform expressions into values, and a "quotation"
phase where we reflect values back into expressions. As the values are
meant to represent the _semantics_ of an expression, each equivalence
class of expressions ought to be mapped to the same value during
evaluation. The quotation phase then plucks out a canonical
representative for each one of these equivalence classes, which
then becomes our normal form.

The particular variant of NbE that we are using is known as *Typed NbE*.
What distinguishes it from *Untyped NbE* is the treatment of quotation.
In Untyped NbE, quotation proceeds in a syntax-directed manner, which
makes the enaction of Î·-laws[^eta] more difficult. On the other hand,
if we quote in a type directed manner, we can perform Î·-expansion
at every possible opportunity, which simplifies the implementation
considerably. This will result in larger normal forms, but the
expressions the solver needs to deal with are small, so this isn't
a pressing issue.

[category solver]: Cat.Solver.html
[^eta]: In our context, an Î·-law is something like `
âŸ¨ Ï€â‚ âˆ˜ f , Ï€â‚‚ âˆ˜ f âŸ© â‰¡ f`, where we have an introduction form
wrapping a bunch of eliminators applied to the same expression.

Next, we define an interpretation of expressions back into morphisms.
This will be used to state the all-important soundness theorem.

```agda
  âŸ¦_âŸ§â‚‘ : âˆ€ {X Y} â†’ Expr X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ â€¶idâ€¶ âŸ§â‚‘ = id
  âŸ¦ e1 â€¶âˆ˜â€¶ e2 âŸ§â‚‘ = âŸ¦ e1 âŸ§â‚‘ âˆ˜ âŸ¦ e2 âŸ§â‚‘
  âŸ¦ â€¶Ï€â‚â€¶ âŸ§â‚‘ = Ï€â‚
  âŸ¦ â€¶Ï€â‚‚â€¶ âŸ§â‚‘ = Ï€â‚‚
  âŸ¦ â€¶âŸ¨ e1 , e2 âŸ©â€¶ âŸ§â‚‘ = âŸ¨ âŸ¦ e1 âŸ§â‚‘ , âŸ¦ e2 âŸ§â‚‘ âŸ©
  âŸ¦ â€¶ f â€¶ âŸ§â‚‘ = f
```

## Values

Next, we define a type of *Values*. The goal here is to ensure that we
can't have any eliminators (in our case, projections) applied to
introduction forms (in our case, `âŸ¨_,_âŸ©`{.Agda}). We also need to handle
the normal associativity/identity equations, but those will be handled
by evaluating our expressions into presheaves.

```agda
  data Value : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” â„“) where
    vhom  : âˆ€ {X Y} â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’ â†’ Value X Y
    vpair : âˆ€ {X Y Z} â†’ Value X Y â†’ Value X Z â†’ Value X (Y â€¶âŠ—â€¶ Z)
```

We now define our eliminators for values.

```agda
  vfst : âˆ€ {X Y Z} â†’ Value X (Y â€¶âŠ—â€¶ Z) â†’ Value X Y
  vfst (vhom f) = vhom (Ï€â‚ âˆ˜ f)
  vfst (vpair v1 v2) = v1

  vsnd : âˆ€ {X Y Z} â†’ Value X (Y â€¶âŠ—â€¶ Z) â†’ Value X Z
  vsnd (vhom f) = vhom (Ï€â‚‚ âˆ˜ f)
  vsnd (vpair v1 v2) = v2

  vid : âˆ€ {X} â†’ Value X X
  vid = vhom id
```

## Quotation

As noted above, our quotation is type-directed to make applying Î·-laws
easier. When we encounter a `v : Value X (Y â€¶âŠ—â€¶ Z)`, we will always
Î·-expand it using the eliminators defined above. If `v` is a
`vpair`{.Agda}, then the eliminators will compute away, and we will be
left with the same value we started with. If `v` is a `vhom`{.Agda},
then we will have Î·-expanded it, so all of our normal forms will be
/fully/ Î·-expanded.

As a terminological note, we call this function `reflect` because
`quote` is a reserved keyword in Agda.

```agda
  reflect : âˆ€ X Y â†’ Value X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  reflect X (Y â€¶âŠ—â€¶ Z) v = âŸ¨ (reflect X Y (vfst v)) , reflect X Z (vsnd v) âŸ©
  reflect X â€¶ Y â€¶ (vhom f) = f
```


## Evaluation

Evaluation operates in much the same way as the [category solver],
where we evaluate to `Value X Y â†’ Value X Z` instead of just `Value Y Z`.
This allows us to apply the associativity/identity equations, as well
as the equation that `âŸ¨ f , g âŸ© âˆ˜ h â‰¡ âŸ¨ f âˆ˜ h , g âˆ˜ h âŸ©`.

[category solver]: Cat.Solver.html

```agda
  eval : âˆ€ {X Y Z} â†’ Expr Y Z â†’ Value X Y â†’ Value X Z
  eval â€¶idâ€¶ v = v
  eval (e1 â€¶âˆ˜â€¶ e2) v = eval e1 (eval e2 v)
  eval â€¶Ï€â‚â€¶ v = vfst v
  eval â€¶Ï€â‚‚â€¶ v = vsnd v
  eval â€¶âŸ¨ e1 , e2 âŸ©â€¶ v = vpair (eval e1 v) (eval e2 v)
  eval â€¶ f â€¶ v = vhom (f âˆ˜ reflect _ _ v)
```

As noted earlier, we obtain normal forms by evaluating then quoting.

```agda
  nf : âˆ€ X Y â†’ Expr X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  nf X Y e = reflect X Y (eval e vid)
```

## Soundness

Before proving soundness, we need to prove the normal battery of random
lemmas. The first states that quoting a `vhom f` gives us back `f`.

```agda
  vhom-sound : âˆ€ X Y â†’ (f : Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’) â†’ reflect X Y (vhom f) â‰¡ f
  vhom-sound X (Y â€¶âŠ—â€¶ Z) f =
    âŸ¨ reflect X Y (vhom (Ï€â‚ âˆ˜ f)) , reflect X Z (vhom (Ï€â‚‚ âˆ˜ f)) âŸ© â‰¡âŸ¨ apâ‚‚ âŸ¨_,_âŸ© (vhom-sound X Y (Ï€â‚ âˆ˜ f)) (vhom-sound X Z (Ï€â‚‚ âˆ˜ f)) âŸ©
    âŸ¨ Ï€â‚ âˆ˜ f , Ï€â‚‚ âˆ˜ f âŸ©                                           â‰¡Ë˜âŸ¨ âŸ¨âŸ©-unique refl refl âŸ©
    f                                                             âˆ
  vhom-sound X â€¶ x â€¶ f = refl
```

Next, some soundless lemmas for our eliminators. We want to show that
applying each eliminator to a value corresponds to the correct thing
once interpreted into our category `ğ’`.

```agda
  vfst-sound : âˆ€ X Y Z â†’ (v : Value X (Y â€¶âŠ—â€¶ Z)) â†’ reflect X Y (vfst v) â‰¡ Ï€â‚ âˆ˜ reflect X (Y â€¶âŠ—â€¶ Z) v
  vfst-sound X Y Z (vhom f) =
    reflect X Y (vhom (Ï€â‚ âˆ˜ f))       â‰¡âŸ¨ vhom-sound X Y (Ï€â‚ âˆ˜ f) âŸ©
    Ï€â‚ âˆ˜ f                            â‰¡Ë˜âŸ¨ reflâŸ©âˆ˜âŸ¨ vhom-sound X (Y â€¶âŠ—â€¶ Z) f âŸ©
    Ï€â‚ âˆ˜ reflect X (Y â€¶âŠ—â€¶ Z) (vhom f) âˆ
  vfst-sound X Y Z (vpair v1 v2) =
    reflect X Y v1                               â‰¡Ë˜âŸ¨ Ï€â‚âˆ˜âŸ¨âŸ© âŸ©
    Ï€â‚ âˆ˜ âŸ¨ (reflect X Y v1) , (reflect X Z v2) âŸ© âˆ

  vsnd-sound : âˆ€ X Y Z â†’ (v : Value X (Y â€¶âŠ—â€¶ Z)) â†’ reflect X Z (vsnd v) â‰¡ Ï€â‚‚ âˆ˜ reflect X (Y â€¶âŠ—â€¶ Z) v
  vsnd-sound X Y Z (vhom f) =
    reflect X Z (vhom (Ï€â‚‚ âˆ˜ f))       â‰¡âŸ¨ vhom-sound X Z (Ï€â‚‚ âˆ˜ f) âŸ©
    Ï€â‚‚ âˆ˜ f                            â‰¡Ë˜âŸ¨ reflâŸ©âˆ˜âŸ¨ vhom-sound X (Y â€¶âŠ—â€¶ Z) f âŸ©
    Ï€â‚‚ âˆ˜ reflect X (Y â€¶âŠ—â€¶ Z) (vhom f) âˆ
  vsnd-sound X Y Z (vpair v1 v2) =
    reflect X Z v2                               â‰¡Ë˜âŸ¨ Ï€â‚‚âˆ˜âŸ¨âŸ© âŸ©
    Ï€â‚‚ âˆ˜ âŸ¨ (reflect X Y v1) , (reflect X Z v2) âŸ© âˆ
```

We handle composition of values by interpreting expressions as functions
/between/ values. So in a sense, this following lemma is a proof of
soundness for our interpretation of composition.

```agda
  sound-k : âˆ€ X Y Z â†’ (e : Expr Y Z) â†’ (v : Value X Y)
          â†’ reflect X Z (eval e v) â‰¡ âŸ¦ e âŸ§â‚‘ âˆ˜ reflect X Y v
  sound-k X Y Y â€¶idâ€¶ v = sym (idl _)
  sound-k X Y Z (e1 â€¶âˆ˜â€¶ e2) v =
    reflect X Z (eval e1 (eval e2 v)) â‰¡âŸ¨ sound-k X _ Z e1 (eval e2 v) âŸ©
    âŸ¦ e1 âŸ§â‚‘ âˆ˜ reflect X _ (eval e2 v) â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ sound-k X Y _ e2 v âŸ©
    âŸ¦ e1 âŸ§â‚‘ âˆ˜ âŸ¦ e2 âŸ§â‚‘ âˆ˜ reflect X Y v â‰¡âŸ¨ assoc _ _ _ âŸ©
    âŸ¦ e1 â€¶âˆ˜â€¶ e2 âŸ§â‚‘ âˆ˜ reflect X Y v    âˆ
  sound-k X (Y â€¶âŠ—â€¶ Z) Y â€¶Ï€â‚â€¶ v = vfst-sound X Y Z v
  sound-k X (Y â€¶âŠ—â€¶ Z) Z â€¶Ï€â‚‚â€¶ v = vsnd-sound X Y Z v
  sound-k X Y (Z1 â€¶âŠ—â€¶ Z2) â€¶âŸ¨ e1 , e2 âŸ©â€¶ v =
    âŸ¨ reflect X Z1 (eval e1 v) , reflect X Z2 (eval e2 v) âŸ© â‰¡âŸ¨ apâ‚‚ âŸ¨_,_âŸ© (sound-k X Y Z1 e1 v) (sound-k X Y Z2 e2 v) âŸ©
    âŸ¨ âŸ¦ e1 âŸ§â‚‘ âˆ˜ reflect X Y v , âŸ¦ e2 âŸ§â‚‘ âˆ˜ reflect X Y v âŸ©   â‰¡Ë˜âŸ¨ âŸ¨âŸ©âˆ˜ _ âŸ©
    âŸ¨ âŸ¦ e1 âŸ§â‚‘ , âŸ¦ e2 âŸ§â‚‘ âŸ© âˆ˜ reflect X Y v                   âˆ
  sound-k X Y Z â€¶ x â€¶ v = vhom-sound X Z _
```

The final soundness proof: normalizing an expression gives us the same
morphism as naively interpreting the expression.

```agda
  sound : âˆ€ X Y â†’ (e : Expr X Y) â†’ nf X Y e â‰¡ âŸ¦ e âŸ§â‚‘
  sound X Y e = sound-k X X Y e vid âˆ™ elimr (vhom-sound X X id)
```

## Solver interface

In order to make the reflection easier later, we bundle up the soundness
proof. Marking this as abstract is *very important*. This prevents
agda from unfolding into an absolutely enormous proof when used as
a macro, which is critical for performance.

```agda
  abstract
    solve : âˆ€ X Y â†’ (e1 e2 : Expr X Y) â†’ nf X Y e1 â‰¡ nf X Y e2 â†’ âŸ¦ e1 âŸ§â‚‘ â‰¡ âŸ¦ e2 âŸ§â‚‘
    solve X Y e1 e2 p = sym (sound X Y e1) Â·Â· p Â·Â· sound X Y e2
```

# Reflection

As per usual, this is the hard part. Reflection is normally quite tricky, but the
situation here is even harder than the category solver, as we need to reflect
on objects as well as morphisms.

We begin by defining a bunch of pattern synonyms for matching on various fields
of precategories, as well as objects + morphisms that arise from the product structure.

The situation here is extremely fiddly when it comes to implicit arguments, as
we not only need to get the number correct, but also their multiplicity. Record
projections always mark the records parameters as `hidden`{.Agda} and
`quantity-0`{.Agda}, so we need to take care to do the same in these patterns.

```agda
module Reflection where
  private
    pattern is-product-field X Y args =
      _ hmâˆ· _ hmâˆ· _ hmâˆ· -- category args
      X hmâˆ· Y hmâˆ·       -- objects of product
      _ hmâˆ·             -- apex
      _ hmâˆ· _ hmâˆ·       -- projections
      _ vâˆ·              -- is-product record argument
      args
    pattern product-field X Y args =
      _ hmâˆ· _ hmâˆ· _ hmâˆ· -- category args
      X hmâˆ· Y hmâˆ·       -- objects of product
      _ vâˆ·              -- product record argument
      args
    pattern category-field args = _ hmâˆ· _ hmâˆ· _ vâˆ· args

    pattern â€œâŠ—â€ X Y =
      def (quote Product.apex) (product-field X Y [])
    pattern â€œidâ€ X =
      def (quote Precategory.id) (category-field (X hâˆ· []))
    pattern â€œâˆ˜â€ X Y Z f g =
      def (quote Precategory._âˆ˜_) (category-field (X hâˆ· Y hâˆ· Z hâˆ· f vâˆ· g vâˆ· []))
    pattern â€œÏ€â‚â€ X Y =
      def (quote (Product.Ï€â‚)) (product-field X Y [])
    pattern â€œÏ€â‚‚â€ X Y =
      def (quote (Product.Ï€â‚‚)) (product-field X Y [])
    pattern â€œâŸ¨âŸ©â€ X Y Z f g =
      def (quote (is-product.âŸ¨_,_âŸ©)) (is-product-field Y Z (X hâˆ· f vâˆ· g vâˆ· []))
```

Next, we define some helpers to make constructing things in the
`NbE`{.Agda} module easier.

```agda
    mk-nbe-con : Name â†’ List (Arg Term) â†’ Term
    mk-nbe-con con-name args =
      con con-name (unknown hâˆ· unknown hâˆ· unknown hâˆ· unknown hâˆ· args)

    mk-nbe-call : Term â†’ Term â†’ List (Arg Term) â†’ List (Arg Term)
    mk-nbe-call cat cart args = unknown hâˆ· unknown hâˆ· cat vâˆ· cart vâˆ· args
```

We also define some helpers for building quoted calls to
`NbE.nf`{.Agda} and `NbE.solve`{.Agda}.

```agda
  â€œnfâ€ : Term â†’ Term â†’ Term â†’ Term â†’ Term â†’ Term
  â€œnfâ€ cat cart x y e =
    def (quote NbE.nf) (mk-nbe-call cat cart (x vâˆ· y vâˆ· e vâˆ· []))

  â€œsolveâ€ : Term â†’ Term â†’ Term â†’ Term â†’ Term â†’ Term â†’ Term
  â€œsolveâ€ cat cart x y lhs rhs =
    def (quote NbE.solve) $
      mk-nbe-call cat cart (x vâˆ· y vâˆ· lhs vâˆ· rhs vâˆ· â€œreflâ€ vâˆ· [])
```

Now for the meat of the reflection. `build-obj-expr` will construct
quoted terms of type `NbE.â€¶Obâ€¶`{.Agda} from quoted terms of type
`Precategory.Ob`{.Agda}. `build-hom-expr` does the same thing, but for
`NbE.Expr`{.Agda} and `Precategory.Hom`{.Agda}.

Note that we apply all implicits to constructors in `build-hom-expr`.
If we don't do this, Agda will get *very* upset.

```agda
  build-obj-expr : Term â†’ Term
  build-obj-expr (â€œâŠ—â€ X Y)  =
    con (quote NbE.â€¶Obâ€¶._â€¶âŠ—â€¶_) (build-obj-expr X vâˆ· build-obj-expr Y vâˆ· [])
  build-obj-expr X =
    con (quote NbE.â€¶Obâ€¶.â€¶_â€¶) (X vâˆ· [])

  build-hom-expr : Term â†’ Term
  build-hom-expr (â€œidâ€ X) =
    mk-nbe-con (quote NbE.Expr.â€¶idâ€¶) $
      build-obj-expr X hâˆ· []
  build-hom-expr (â€œâˆ˜â€ X Y Z f g) =
    mk-nbe-con (quote NbE.Expr._â€¶âˆ˜â€¶_) $
      build-obj-expr X hâˆ· build-obj-expr Y hâˆ· build-obj-expr Z hâˆ·
      build-hom-expr f vâˆ· build-hom-expr g vâˆ· []
  build-hom-expr (â€œÏ€â‚â€ X Y) =
    mk-nbe-con (quote NbE.Expr.â€¶Ï€â‚â€¶) $
      build-obj-expr X hâˆ· build-obj-expr Y hâˆ· []
  build-hom-expr (â€œÏ€â‚‚â€ X Y) =
    mk-nbe-con (quote NbE.Expr.â€¶Ï€â‚‚â€¶) $
      build-obj-expr X hâˆ· build-obj-expr Y hâˆ· []
  build-hom-expr (â€œâŸ¨âŸ©â€ X Y Z f g) =
    mk-nbe-con (quote NbE.Expr.â€¶âŸ¨_,_âŸ©â€¶) $
    build-obj-expr X hâˆ· build-obj-expr Y hâˆ· build-obj-expr Z hâˆ·
    build-hom-expr f vâˆ· build-hom-expr g vâˆ· []
  build-hom-expr f =
    con (quote NbE.Expr.â€¶_â€¶) (f vâˆ· [])
```

Now, for the solver interface. This follows the usual pattern: we create
a list of names that we will pass to `withReduceDefs`{.Agda}, which will
prevent Agda from normalizing away the things we want to reflect upon.

```agda
  dont-reduce : List Name
  dont-reduce =
    quote Precategory.Hom âˆ·
    quote Precategory.id âˆ·
    quote Precategory._âˆ˜_ âˆ·
    quote Product.apex âˆ·
    quote Product.Ï€â‚ âˆ·
    quote Product.Ï€â‚‚ âˆ·
    quote is-product.âŸ¨_,_âŸ© âˆ· []
```

We will need to recover the objects from some quoted hom to make the
calls to the solver/normaliser.

```agda
  get-objects : Term â†’ TC (Term Ã— Term)
  get-objects tm = ((infer-type tm >>= normalise) >>= wait-just-a-bit) >>= Î» where
    (def (quote Precategory.Hom) (category-field (x vâˆ· y vâˆ· []))) â†’
      pure (x , y)
    tp â†’
      typeError $ strErr "Can't determine objects: " âˆ· termErr tp âˆ· []
```

We also make some debugging macros, which are very useful for when you
want to examine the exact quoted representations of objects/homs.

```agda
  obj-repr-macro : âˆ€ {o â„“} (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y) â†’ Term â†’ Term â†’ TC âŠ¤
  obj-repr-macro cat cart hom hole =
    withReconstructed true $
    withNormalisation false $
    withReduceDefs (false , dont-reduce) $ do
    (x , y) â† get-objects hom
    â€œxâ€ â† build-obj-expr <$> normalise x
    â€œyâ€ â† build-obj-expr <$> normalise y
    typeError $ strErr "Determined objects of " âˆ· termErr hom âˆ· strErr " to be\n  " âˆ·
                termErr x âˆ· strErr "\nAnd\n  " âˆ·
                termErr y âˆ· strErr "\nWith quoted representations\n  " âˆ·
                termErr â€œxâ€ âˆ· strErr "\nAnd\n  " âˆ·
                termErr â€œyâ€ âˆ· []

  hom-repr-macro : âˆ€ {o â„“} (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y) â†’ Term â†’ Term â†’ TC âŠ¤
  hom-repr-macro cat cart hom hole =
    withReconstructed true $
    withNormalisation false $
    withReduceDefs (false , dont-reduce) $ do
    (x , y) â† get-objects hom
    â€œxâ€ â† build-obj-expr <$> normalise x
    â€œyâ€ â† build-obj-expr <$> normalise y
    â€œhomâ€ â† build-hom-expr <$> normalise hom
    typeError $ strErr "The morphism\n  " âˆ·
                termErr hom âˆ· strErr "\nis represented by\n  " âˆ·
                termErr â€œhomâ€ âˆ· strErr "\nwith objects\n  " âˆ·
                termErr â€œxâ€ âˆ· strErr "\nAnd\n  " âˆ·
                termErr â€œyâ€ âˆ· []
```

Now, the simplifier and solver reflection. This just puts together
all of our bits from before.

There is one subtlety here with regards to `withReconstructed`.
We are reflecting on the record parameters to `Product`{.Agda} and
`is-product`{.Agda} to determine the objects involved in things like `âŸ¨_,_âŸ©`{.Agda},
which Agda will mark as `unknown` by default. This will cause `build-obj-expr`{.Agda}
to then fail when we have expressions involving nested `_âŠ—_`{.Agda}.
Wrapping everything in `withReconstructed` causes Agda to fill in these arguments
with their actual values, which then fixes the issue.

```agda
  simpl-macro : âˆ€ {o â„“} (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y) â†’ Term â†’ Term â†’ TC âŠ¤
  simpl-macro cat cart hom hole =
    withReconstructed true $
    withNormalisation false $
    withReduceDefs (false , dont-reduce) $ do
    (x , y) â† get-objects hom
    â€œxâ€ â† build-obj-expr <$> normalise x
    â€œyâ€ â† build-obj-expr <$> normalise y
    â€œhomâ€ â† build-hom-expr <$> normalise hom
    â€œcatâ€ â† quoteTC cat
    â€œcartâ€ â† quoteTC cart
    unify hole (â€œnfâ€ â€œcatâ€ â€œcartâ€ â€œxâ€ â€œyâ€ â€œhomâ€)
```

Finally, we define the user-facing interface as a series of macros.

```agda
macro
  products-obj-repr! : âˆ€ {o â„“}
                       â†’ (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y)
                       â†’ Term â†’ Term â†’ TC âŠ¤
  products-obj-repr! = Reflection.obj-repr-macro

  products-repr! : âˆ€ {o â„“}
                   â†’ (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y)
                   â†’ Term â†’ Term â†’ TC âŠ¤
  products-repr! = Reflection.hom-repr-macro

  products-simpl! : âˆ€ {o â„“}
                    â†’ (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y)
                    â†’ Term â†’ Term â†’ TC âŠ¤
  products-simpl! = Reflection.simpl-macro
```

<!--
```agda
module _ {o â„“} {C : Precategory o â„“} (cart : âˆ€ X Y â†’ Product C X Y) {x y : âŒ C âŒŸ} {h1 h2 : C .Precategory.Hom x y} where
  open Reflection

  private
    products-worker : Term â†’ TC âŠ¤
    products-worker goal = withReconstructed true $ withNormalisation true $ withReduceDefs (false , dont-reduce) do
      `h1 â† wait-for-type =<< quoteTC h1
      `h2 â† quoteTC h2
      `x â† quoteTC x
      `y â† quoteTC y

      â€œcartâ€ â† quoteTC cart

      let
        â€œxâ€   = build-obj-expr `x
        â€œyâ€   = build-obj-expr `y
        â€œlhsâ€ = build-hom-expr `h1
        â€œrhsâ€ = build-hom-expr `h2

      unify goal (Reflection.â€œsolveâ€ unknown â€œcartâ€ â€œxâ€ â€œyâ€ â€œlhsâ€ â€œrhsâ€) <|> do
        â€œcatâ€ â† quoteTC C
        vlhs â† normalise (â€œnfâ€ â€œcatâ€ â€œcartâ€ â€œxâ€ â€œyâ€ â€œlhsâ€)
        vrhs â† normalise (â€œnfâ€ â€œcatâ€ â€œcartâ€ â€œxâ€ â€œyâ€ â€œrhsâ€)
        typeError
          [ "Could not equate the following expressions:\n  "
          , termErr `h1 , "\nAnd\n  " , termErr `h2
          , "\nReflected expressions\n  "
          , termErr â€œlhsâ€ , "\nAnd\n  " , termErr â€œrhsâ€
          , strErr "\nComputed normal forms\n  "
          , termErr vlhs , strErr "\nAnd\n  " , termErr vrhs
          ]

  products! : {@(tactic products-worker) p : h1 â‰¡ h2} â†’ h1 â‰¡ h2
  products! {p = p} = p
```
-->

# Demo

Wow, that was a lot of hard work! Let's marvel at the fruits of our labor.

```agda
private module Tests {o â„“} (ğ’ : Precategory o â„“) (cartesian : âˆ€ X Y â†’ Product ğ’ X Y) where
  open Precategory ğ’
  open Binary-products ğ’ cartesian
  open NbE ğ’ cartesian

  test-Î· : âˆ€ {X Y Z} â†’ (f : Hom X (Y âŠ—â‚€ Z))
           â†’ f â‰¡ âŸ¨ Ï€â‚ âˆ˜ f , Ï€â‚‚ âˆ˜ f âŸ©
  test-Î· f = products! cartesian

  test-Î²â‚ : âˆ€ {X Y Z} â†’ (f : Hom X Y) â†’ (g : Hom X Z)
            â†’ Ï€â‚ âˆ˜ âŸ¨ f , g âŸ© â‰¡ f
  test-Î²â‚ f g = products! cartesian

  test-Î²â‚‚ : âˆ€ {X Y Z} â†’ (f : Hom X Y) â†’ (g : Hom X Z)
            â†’ Ï€â‚‚ âˆ˜ âŸ¨ f , g âŸ© â‰¡ g
  test-Î²â‚‚ f g = products! cartesian

  test-âŸ¨âŸ©âˆ˜ : âˆ€ {W X Y Z} â†’ (f : Hom X Y) â†’ (g : Hom X Z) â†’ (h : Hom W X)
             â†’ âŸ¨ f âˆ˜ h , g âˆ˜ h âŸ© â‰¡ âŸ¨ f , g âŸ© âˆ˜ h
  test-âŸ¨âŸ©âˆ˜ f g h = products! cartesian

  -- If you don't have 'withReconstructed' on, this test will fail!
  test-nested : âˆ€ {W X Y Z} â†’ (f : Hom W X) â†’ (g : Hom W Y) â†’ (h : Hom W Z)
             â†’ âŸ¨ âŸ¨ f , g âŸ© , h âŸ© â‰¡ âŸ¨ âŸ¨ f , g âŸ© , h âŸ©
  test-nested {W} {X} {Y} {Z} f g h = products! cartesian

  test-big : âˆ€ {W X Y Z} â†’ (f : Hom (W âŠ—â‚€ X) (W âŠ—â‚€ Y)) â†’ (g : Hom (W âŠ—â‚€ X) Z)
             â†’ (Ï€â‚ âˆ˜ âŸ¨ f , g âŸ©) âˆ˜ id â‰¡ id âˆ˜ âŸ¨ Ï€â‚ , Ï€â‚‚ âŸ© âˆ˜ f
  test-big f g = products! cartesian
```
