module Cat.Diagram.Monad.Solver where

open import 1Lab.Prelude hiding (id; _âˆ˜_)
open import 1Lab.Reflection

open import Cat.Base
open import Cat.Diagram.Monad

import Cat.Functor.Reasoning as FR
import Cat.Reasoning as CR

module NbE {o h} {ğ’ : Precategory o h} (M : Monad ğ’) where
  open CR ğ’
  module M = FR (Monad.M M)
  open Monad M

  --------------------------------------------------------------------------------
  -- NOTE: Object Expressions
  -- We canâ€²t index everyting by 'Ob', as Agda will (rightfully) assume that Mâ‚€ is not injective,
  -- which then inhibits on our ability to pattern match on things.
  -- Therefore, we introduce a reflected type of object expressions,
  -- which solves the injectivity issue.

  data â€¶Obâ€¶ : Type o where
    â€¶_â€¶   : Ob â†’ â€¶Obâ€¶
    â€¶Mâ‚€â€¶ : â€¶Obâ€¶ â†’ â€¶Obâ€¶

  âŸ¦_âŸ§â‚’ : â€¶Obâ€¶ â†’ Ob
  âŸ¦ â€¶ X â€¶ âŸ§â‚’ = X
  âŸ¦ â€¶Mâ‚€â€¶ X âŸ§â‚’ = Mâ‚€ âŸ¦ X âŸ§â‚’

  private variable
    X Y Z : â€¶Obâ€¶

  data â€¶Homâ€¶ : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    â€¶Mâ‚â€¶  : â€¶Homâ€¶ X Y â†’ â€¶Homâ€¶ (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
    â€¶Î·â€¶   : (X : â€¶Obâ€¶) â†’ â€¶Homâ€¶ X (â€¶Mâ‚€â€¶ X)
    â€¶Î¼â€¶   : (X : â€¶Obâ€¶) â†’ â€¶Homâ€¶ (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ X)) (â€¶Mâ‚€â€¶ X)
    _â€¶âˆ˜â€¶_ : â€¶Homâ€¶ Y Z â†’ â€¶Homâ€¶ X Y â†’ â€¶Homâ€¶ X Z
    â€¶idâ€¶  : â€¶Homâ€¶ X X
    _â†‘    : âˆ€ {X Y} â†’ Hom X Y â†’ â€¶Homâ€¶ â€¶ X â€¶ â€¶ Y â€¶

  âŸ¦_âŸ§â‚• : â€¶Homâ€¶ X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ â€¶Mâ‚â€¶ f âŸ§â‚• = Mâ‚ âŸ¦ f âŸ§â‚•
  âŸ¦ â€¶Î·â€¶ X âŸ§â‚• = unit.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ â€¶Î¼â€¶ X âŸ§â‚• = mult.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ e1 â€¶âˆ˜â€¶ e2 âŸ§â‚• = âŸ¦ e1 âŸ§â‚• âˆ˜ âŸ¦ e2 âŸ§â‚•
  âŸ¦ â€¶idâ€¶ âŸ§â‚• = id
  âŸ¦ f â†‘ âŸ§â‚• = f

  --------------------------------------------------------------------------------
  -- Values

  data Frame : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    khom  : âˆ€ {X Y} â†’ Hom X Y â†’ Frame â€¶ X â€¶ â€¶ Y â€¶
    kmap  : Frame X Y â†’ Frame (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
    kunit : (X : â€¶Obâ€¶) â†’ Frame X (â€¶Mâ‚€â€¶ X)
    kmult : (X : â€¶Obâ€¶) â†’ Frame (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ X)) (â€¶Mâ‚€â€¶ X)

  data Value : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    vid : Value X X
    vcomp : Frame Y Z â†’ Value X Y â†’ Value X Z

  âŸ¦_âŸ§â‚– : Frame X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ khom f âŸ§â‚– = f
  âŸ¦ kmap k âŸ§â‚– = Mâ‚ âŸ¦ k âŸ§â‚–
  âŸ¦ kunit X âŸ§â‚– = unit.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ kmult X âŸ§â‚– = mult.Î· âŸ¦ X âŸ§â‚’

  âŸ¦_âŸ§áµ¥ : Value X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ vid âŸ§áµ¥ = id
  âŸ¦ vcomp k v âŸ§áµ¥ = âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥

  --------------------------------------------------------------------------------
  -- Evaluation
  --
  -- The evaluation strategy here is a bit subtle. The naive option
  -- is to push the 'kunit' frames all the way to the bottom of the stack,
  -- but this makes enacting the 'Î¼ âˆ˜ Î·' equations inneficient, as that
  -- means we will also have to push the 'kmult' frames all the way to the bottom
  -- as well.
  --
  -- Instead, what we do is push the 'kmap' frames past all of the 'kunit' and 'kmult'
  -- frames, which ensures that all of the 'kunit' and 'kmult' frames remain on the top
  -- of the stack. This makes it easier to enact the equations in question, as
  -- we don't have to dig nearly as far.

  do-vmap : Value X Y â†’ Value (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
  do-vmap vid = vid
  do-vmap (vcomp f v) = vcomp (kmap f) (do-vmap v)

  push-unit : Value X Y â†’ Value X (â€¶Mâ‚€â€¶ Y)
  push-unit vid = vcomp (kunit _) vid
  push-unit (vcomp k v) = vcomp (kmap k) (push-unit v)

  push-kmap : Frame Y Z â†’ Value X (â€¶Mâ‚€â€¶ Y) â†’ Value X (â€¶Mâ‚€â€¶ Z)
  push-kmult : Value X (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ Y)) â†’ Value X (â€¶Mâ‚€â€¶ Y)
  push-frm : Frame Y Z â†’ Value X Y â†’ Value X Z

  push-kmap k vid = vcomp (kmap k) vid
  push-kmap k (vcomp (kmap k') v) = vcomp (kmap k) (vcomp (kmap k') v)
  push-kmap k (vcomp (kunit _) v) = vcomp (kunit _) (push-frm k v)
  push-kmap k (vcomp (kmult _) v) = vcomp (kmult _) (push-kmap (kmap k) v)

  push-kmult vid = vcomp (kmult _) vid
  push-kmult (vcomp (kmap (kmap k)) v) = vcomp (kmult _) (vcomp (kmap (kmap k)) v)
  push-kmult (vcomp (kmap (kunit _)) v) = v
  push-kmult (vcomp (kmap (kmult _)) v) = vcomp (kmult _) (vcomp (kmult _) v)
  push-kmult (vcomp (kunit _) v) = v
  push-kmult (vcomp (kmult _) v) = vcomp (kmult _) (vcomp (kmult _) v)

  push-frm (khom f) v = vcomp (khom f) v
  push-frm (kmap k) v = push-kmap k v
  push-frm (kunit _) v = vcomp (kunit _) v
  push-frm (kmult _) v = push-kmult v

  do-vcomp : Value Y Z â†’ Value X Y â†’ Value X Z
  do-vcomp vid v2 = v2
  do-vcomp (vcomp k v1) v2 = push-frm k (do-vcomp v1 v2)

  eval : â€¶Homâ€¶ X Y â†’ Value X Y
  eval (â€¶Mâ‚â€¶ e) = do-vmap (eval e)
  eval (â€¶Î·â€¶ X) = vcomp (kunit X) vid
  eval (â€¶Î¼â€¶ X) = vcomp (kmult X) vid
  eval (e1 â€¶âˆ˜â€¶ e2) = do-vcomp (eval e1) (eval e2)
  eval â€¶idâ€¶ = vid
  eval (f â†‘) = vcomp (khom f) vid

  --------------------------------------------------------------------------------
  -- Soundness

  vmap-sound : âˆ€ (v : Value X Y) â†’ âŸ¦ do-vmap v âŸ§áµ¥ â‰¡ Mâ‚ âŸ¦ v âŸ§áµ¥
  vmap-sound vid = sym M-id
  vmap-sound (vcomp k v) =
    Mâ‚ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ do-vmap v âŸ§áµ¥ â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ vmap-sound v âŸ©
    Mâ‚ âŸ¦ k âŸ§â‚– M.ğ’Ÿ.âˆ˜ Mâ‚ âŸ¦ v âŸ§áµ¥  â‰¡Ë˜âŸ¨ M-âˆ˜ âŸ¦ k âŸ§â‚– âŸ¦ v âŸ§áµ¥ âŸ©
    Mâ‚ (âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥) âˆ

  push-kmap-sound  : âˆ€ (k : Frame Y Z) â†’ (v : Value X (â€¶Mâ‚€â€¶ Y)) â†’ âŸ¦ push-kmap k v âŸ§áµ¥ â‰¡ Mâ‚ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥
  push-kmult-sound : (v : Value X (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ Y))) â†’ âŸ¦ push-kmult v âŸ§áµ¥ â‰¡ mult.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ v âŸ§áµ¥
  push-frm-sound   : âˆ€ (k : Frame Y Z) â†’ (v : Value X Y) â†’ âŸ¦ push-frm k v âŸ§áµ¥ â‰¡ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥

  push-kmap-sound k vid = refl
  push-kmap-sound k (vcomp (kmap k') v) = refl
  push-kmap-sound {Y = Y} {Z = Z} {X = X} k (vcomp (kunit Y) v) =
    unit.Î· âŸ¦ Z âŸ§â‚’ âˆ˜ âŸ¦ push-frm k v âŸ§áµ¥      â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ push-frm-sound k v âŸ©
    unit.Î· âŸ¦ Z âŸ§â‚’  âˆ˜ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥       â‰¡âŸ¨ extendl (unit.is-natural âŸ¦ Y âŸ§â‚’ âŸ¦ Z âŸ§â‚’ âŸ¦ k âŸ§â‚–) âŸ©
    Mâ‚ âŸ¦ k âŸ§â‚– âˆ˜ unit.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ v âŸ§áµ¥     âˆ
  push-kmap-sound {Y = Y} {Z = Z} {X = X} k (vcomp (kmult Y) v) =
    mult.Î· âŸ¦ Z âŸ§â‚’ âˆ˜ âŸ¦ push-kmap (kmap k) v âŸ§áµ¥ â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ push-kmap-sound (kmap k) v âŸ©
    mult.Î· âŸ¦ Z âŸ§â‚’ âˆ˜ Mâ‚ (Mâ‚ âŸ¦ k âŸ§â‚–) âˆ˜ âŸ¦ v âŸ§áµ¥   â‰¡âŸ¨ extendl (mult.is-natural âŸ¦ Y âŸ§â‚’ âŸ¦ Z âŸ§â‚’ âŸ¦ k âŸ§â‚–) âŸ©
    Mâ‚ âŸ¦ k âŸ§â‚– âˆ˜ mult.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ v âŸ§áµ¥        âˆ

  push-kmult-sound vid = refl
  push-kmult-sound (vcomp (kmap (kmap k)) v) = refl
  push-kmult-sound (vcomp (kmap (kunit _)) v) = insertl left-ident
  push-kmult-sound (vcomp (kmap (kmult _)) v) = extendl (sym mult-assoc)
  push-kmult-sound (vcomp (kunit _) v) = insertl right-ident
  push-kmult-sound (vcomp (kmult _) v) = refl

  push-frm-sound (khom f) v = refl
  push-frm-sound (kmap k) v = push-kmap-sound k v
  push-frm-sound (kunit X) v = refl
  push-frm-sound (kmult X) v = push-kmult-sound v

  vcomp-sound : âˆ€ (v1 : Value Y Z) â†’ (v2 : Value X Y) â†’ âŸ¦ do-vcomp v1 v2 âŸ§áµ¥ â‰¡ âŸ¦ v1 âŸ§áµ¥ âˆ˜ âŸ¦ v2 âŸ§áµ¥
  vcomp-sound vid v2 = sym (idl âŸ¦ v2 âŸ§áµ¥)
  vcomp-sound (vcomp k v1) v2 =
    âŸ¦ push-frm k (do-vcomp v1 v2) âŸ§áµ¥ â‰¡âŸ¨ push-frm-sound k (do-vcomp v1 v2) âŸ©
    âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ do-vcomp v1 v2 âŸ§áµ¥ â‰¡âŸ¨ pushr (vcomp-sound v1 v2) âŸ©
    (âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v1 âŸ§áµ¥) âˆ˜ âŸ¦ v2 âŸ§áµ¥ âˆ

  eval-sound : âˆ€ (e : â€¶Homâ€¶ X Y) â†’ âŸ¦ eval e âŸ§áµ¥ â‰¡ âŸ¦ e âŸ§â‚•
  eval-sound (â€¶Mâ‚â€¶ e) =
    âŸ¦ do-vmap (eval e) âŸ§áµ¥ â‰¡âŸ¨ vmap-sound (eval e) âŸ©
    Mâ‚ âŸ¦ eval e âŸ§áµ¥        â‰¡âŸ¨ M.âŸ¨ eval-sound e âŸ© âŸ©
    Mâ‚ âŸ¦ e âŸ§â‚• âˆ
  eval-sound (â€¶Î·â€¶ X) = idr (unit.Î· âŸ¦ X âŸ§â‚’)
  eval-sound (â€¶Î¼â€¶ X) = idr (mult.Î· âŸ¦ X âŸ§â‚’)
  eval-sound (e1 â€¶âˆ˜â€¶ e2) =
    âŸ¦ do-vcomp (eval e1) (eval e2) âŸ§áµ¥ â‰¡âŸ¨ vcomp-sound (eval e1) (eval e2) âŸ©
    âŸ¦ eval e1 âŸ§áµ¥ âˆ˜ âŸ¦ eval e2 âŸ§áµ¥       â‰¡âŸ¨ apâ‚‚ _âˆ˜_ (eval-sound e1) (eval-sound e2) âŸ©
    âŸ¦ e1 âŸ§â‚• âˆ˜ âŸ¦ e2 âŸ§â‚•                 âˆ
  eval-sound â€¶idâ€¶ = refl
  eval-sound (f â†‘) = idr f

  abstract
    solve : âˆ€ (e1 e2 : â€¶Homâ€¶ X Y) â†’ âŸ¦ eval e1 âŸ§áµ¥ â‰¡ âŸ¦ eval e2 âŸ§áµ¥ â†’ âŸ¦ e1 âŸ§â‚• â‰¡ âŸ¦ e2 âŸ§â‚•
    solve e1 e2 p = sym (eval-sound e1) Â·Â· p Â·Â· (eval-sound e2)

module Reflection where

  pattern category-args xs =
    _ h0âˆ· _ h0âˆ· _ vâˆ· xs

  pattern functor-args functor xs =
    _ h0âˆ· _ h0âˆ· _ h0âˆ· _ h0âˆ· _ h0âˆ· _ h0âˆ· functor vâˆ· xs

  pattern nat-trans-args nt args =
    _ h0âˆ· _ h0âˆ· _ h0âˆ· _ h0âˆ·
    _ h0âˆ· _ h0âˆ·
    _ h0âˆ· _ h0âˆ·
    nt vâˆ· args
   

  pattern monad-args monad xs =
    _ h0âˆ· _ h0âˆ· _ h0âˆ· monad vâˆ· xs

  pattern monad-fn-args monad xs =
    _ hâˆ· _ hâˆ· _ hâˆ· monad vâˆ· xs

  pattern â€œidâ€ =
    def (quote Precategory.id) (category-args (_ hâˆ· []))

  pattern â€œâˆ˜â€ f g =
    def (quote Precategory._âˆ˜_) (category-args (_ hâˆ· _ hâˆ· _ hâˆ· f vâˆ· g vâˆ· []))

  pattern â€œMâ‚€â€ monad x =
    def (quote Monad.Mâ‚€) (monad-fn-args monad (x vâˆ· []))

  pattern â€œMâ‚â€ monad f =
    def (quote Monad.Mâ‚) (monad-fn-args monad (_ hâˆ· _ hâˆ· f vâˆ· []))

  pattern â€œÎ·â€ monad x =
    def (quote _=>_.Î·) (nat-trans-args (def (quote Monad.unit) (monad-args monad [])) (x vâˆ· []))

  pattern â€œÎ¼â€ monad x =
    def (quote _=>_.Î·) (nat-trans-args (def (quote Monad.mult) (monad-args monad [])) (x vâˆ· []))

  mk-monad-args : Term â†’ List (Arg Term) â†’ List (Arg Term)
  mk-monad-args monad args = unknown h0âˆ· unknown h0âˆ· unknown h0âˆ· monad vâˆ· args

  â€œsolveâ€ : Term â†’ Term â†’ Term â†’ Term
  â€œsolveâ€ monad lhs rhs =
    def (quote NbE.solve) (mk-monad-args monad $ infer-hidden 2 $ lhs vâˆ· rhs vâˆ· def (quote refl) [] vâˆ· [])

  build-object-expr : Term â†’ Term â†’ TC Term
  build-object-expr monad (â€œMâ‚€â€ monad' x) = do
    unify monad monad'
    x â† build-object-expr monad x
    returnTC $ con (quote NbE.â€¶Mâ‚€â€¶) (x vâˆ· [])
  build-object-expr monad x =
    returnTC $ con (quote NbE.â€¶_â€¶) (x vâˆ· [])

  build-hom-expr : Term â†’ Term â†’ TC Term
  build-hom-expr monad â€œidâ€ =
    returnTC $ con (quote NbE.â€¶idâ€¶) []
  build-hom-expr monad (â€œâˆ˜â€ f g) = do
    f â† build-hom-expr monad f
    g â† build-hom-expr monad g
    returnTC $ con (quote NbE._â€¶âˆ˜â€¶_) (f vâˆ· g vâˆ· [])
  build-hom-expr monad (â€œMâ‚â€ monad' f) = do
    unify monad monad'
    f â† build-hom-expr monad f
    returnTC $ con (quote NbE.â€¶Mâ‚â€¶) (f vâˆ· [])
  build-hom-expr monad (â€œÎ·â€ monad' x) = do
    unify monad monad'
    x â† build-object-expr monad x
    returnTC $ con (quote NbE.â€¶Î·â€¶) (x vâˆ· [])
  build-hom-expr monad (â€œÎ¼â€ monad' x) = do
    x â† build-object-expr monad x
    unify monad monad'
    returnTC $ con (quote NbE.â€¶Î¼â€¶) (x vâˆ· [])
  build-hom-expr monad f =
    returnTC $ con (quote NbE._â†‘) (f vâˆ· [])

  dont-reduce : List Name
  dont-reduce =
    quote Precategory.id âˆ· quote Precategory._âˆ˜_ âˆ·
    quote Functor.Fâ‚€ âˆ· quote Functor.Fâ‚ âˆ·
    quote _=>_.Î· âˆ·
    quote Monad.M âˆ· quote Monad.unit âˆ· quote Monad.mult âˆ· []

  solve-macro : âˆ€ {o h} {ğ’ : Precategory o h} â†’ Monad ğ’ â†’ Term â†’ TC âŠ¤
  solve-macro monad hole =
    withNormalisation false $
    dontReduceDefs dont-reduce $ do
      monad-tm â† quoteTC monad
      goal â† inferType hole >>= reduce
      just (lhs , rhs) â† get-boundary goal
        where nothing â†’ typeError $ strErr "Can't determine boundary: " âˆ·
                                    termErr goal âˆ· []
      elhs â† build-hom-expr monad-tm lhs
      erhs â† build-hom-expr monad-tm rhs
      noConstraints $ unify hole (â€œsolveâ€ monad-tm elhs erhs)

macro
  monad! : âˆ€ {o h} {ğ’ : Precategory o h} â†’ Monad ğ’ â†’ Term â†’ TC âŠ¤
  monad! monad = Reflection.solve-macro monad

private module Test {o h} {ğ’ : Precategory o h} (monad : Monad ğ’) where
  open Precategory ğ’
  open Monad monad

  variable
    A B C : Ob

  test : âˆ€ {f : Hom B C} {g : Hom A B}
         â†’ mult.Î· C âˆ˜ Mâ‚ (Mâ‚ (f âˆ˜ g)) âˆ˜ unit.Î· (Mâ‚€ A) â‰¡ Mâ‚ f âˆ˜ Mâ‚ (id âˆ˜ g)
  test = monad! monad

  test-assoc : âˆ€ X â†’ mult.Î· X âˆ˜ Mâ‚ (mult.Î· X) â‰¡ mult.Î· X âˆ˜ mult.Î· (Mâ‚€ X)
  test-assoc X = monad! monad

  test-nested : âˆ€ X â†’ Mâ‚ (mult.Î· X âˆ˜ unit.Î· (Mâ‚€ X)) â‰¡ id
  test-nested _ = monad! monad
     
