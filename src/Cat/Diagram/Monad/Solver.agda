module Cat.Diagram.Monad.Solver where

open import 1Lab.Prelude hiding (id; _âˆ˜_; reflâŸ©âˆ˜âŸ¨_; _âŸ©âˆ˜âŸ¨refl)
open import 1Lab.Reflection hiding (_++_)

open import Cat.Base
open import Cat.Diagram.Monad

import Cat.Functor.Reasoning as FR
import Cat.Reasoning as CR

open import Data.List hiding (_++_)

module NbE {o h} {ğ’ : Precategory o h} (M : Monad ğ’) where
  open CR ğ’
  module M = FR (Monad.M M)
  open Monad M

  --------------------------------------------------------------------------------
  -- NOTE: Object Expressions
  -- We can't index everything by 'Ob', as Agda will (rightfully) assume that Mâ‚€ is not injective,
  -- which then inhibits on our ability to pattern match on things.
  -- Therefore, we introduce a reflected type of object expressions,
  -- which solves the injectivity issue.

  data â€¶Obâ€¶ : Type o where
    â€¶_â€¶   : Ob â†’ â€¶Obâ€¶
    â€¶Mâ‚€â€¶ : â€¶Obâ€¶ â†’ â€¶Obâ€¶

  âŸ¦_âŸ§â‚’ : â€¶Obâ€¶ â†’ Ob
  âŸ¦ â€¶ X â€¶ âŸ§â‚’ = X
  âŸ¦ â€¶Mâ‚€â€¶ X âŸ§â‚’ = Mâ‚€ âŸ¦ X âŸ§â‚’

  private variable
    W X Y Z : â€¶Obâ€¶

  data â€¶Homâ€¶ : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    â€¶Mâ‚â€¶  : â€¶Homâ€¶ X Y â†’ â€¶Homâ€¶ (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
    â€¶Î·â€¶   : (X : â€¶Obâ€¶) â†’ â€¶Homâ€¶ X (â€¶Mâ‚€â€¶ X)
    â€¶Î¼â€¶   : (X : â€¶Obâ€¶) â†’ â€¶Homâ€¶ (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ X)) (â€¶Mâ‚€â€¶ X)
    _â€¶âˆ˜â€¶_ : â€¶Homâ€¶ Y Z â†’ â€¶Homâ€¶ X Y â†’ â€¶Homâ€¶ X Z
    â€¶idâ€¶  : â€¶Homâ€¶ X X
    _â†‘    : âˆ€ {X Y} â†’ Hom X Y â†’ â€¶Homâ€¶ â€¶ X â€¶ â€¶ Y â€¶

  âŸ¦_âŸ§â‚• : â€¶Homâ€¶ X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ â€¶Mâ‚â€¶ f âŸ§â‚• = Mâ‚ âŸ¦ f âŸ§â‚•
  âŸ¦ â€¶Î·â€¶ X âŸ§â‚• = unit.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ â€¶Î¼â€¶ X âŸ§â‚• = mult.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ e1 â€¶âˆ˜â€¶ e2 âŸ§â‚• = âŸ¦ e1 âŸ§â‚• âˆ˜ âŸ¦ e2 âŸ§â‚•
  âŸ¦ â€¶idâ€¶ âŸ§â‚• = id
  âŸ¦ f â†‘ âŸ§â‚• = f

  --------------------------------------------------------------------------------
  -- Values

  data Frame : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    khom  : âˆ€ {X Y} â†’ Hom X Y â†’ Frame â€¶ X â€¶ â€¶ Y â€¶
    kmap  : Frame X Y â†’ Frame (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
    kunit : (X : â€¶Obâ€¶) â†’ Frame X (â€¶Mâ‚€â€¶ X)
    kmult : (X : â€¶Obâ€¶) â†’ Frame (â€¶Mâ‚€â€¶ (â€¶Mâ‚€â€¶ X)) (â€¶Mâ‚€â€¶ X)

  data Value : â€¶Obâ€¶ â†’ â€¶Obâ€¶ â†’ Type (o âŠ” h) where
    [] : Value X X
    _âˆ·_ : Frame Y Z â†’ Value X Y â†’ Value X Z

  infixr 20 _âˆ·_

  âŸ¦_âŸ§â‚– : Frame X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ khom f âŸ§â‚– = f
  âŸ¦ kmap k âŸ§â‚– = Mâ‚ âŸ¦ k âŸ§â‚–
  âŸ¦ kunit X âŸ§â‚– = unit.Î· âŸ¦ X âŸ§â‚’
  âŸ¦ kmult X âŸ§â‚– = mult.Î· âŸ¦ X âŸ§â‚’

  âŸ¦_âŸ§áµ¥ : Value X Y â†’ Hom âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’
  âŸ¦ [] âŸ§áµ¥ = id
  âŸ¦ k âˆ· v âŸ§áµ¥ = âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥

  --------------------------------------------------------------------------------
  -- Evaluation
  --
  -- The evaluation strategy here is a bit subtle. The naive option
  -- is to push the 'kunit' frames all the way to the bottom of the stack,
  -- but this makes enacting the 'Î¼ âˆ˜ Î·' equations inneficient, as that
  -- means we will also have to push the 'kmult' frames all the way to the bottom
  -- as well.
  --
  -- Instead, what we do is push the 'kmap' frames past all of the 'kunit' and 'kmult'
  -- frames, which ensures that all of the 'kunit' and 'kmult' frames remain on the top
  -- of the stack. This makes it easier to enact the equations in question, as
  -- we don't have to dig nearly as far.

  -- Concatenate 2 values together, performing no simplification.
  _++_ : Value Y Z â†’ Value X Y â†’ Value X Z
  [] ++ v2 = v2
  (k âˆ· v1) ++ v2 = k âˆ· (v1 ++ v2)

  -- Apply Mâ‚ to a value.
  do-vmap : Value X Y â†’ Value (â€¶Mâ‚€â€¶ X) (â€¶Mâ‚€â€¶ Y)
  do-vmap [] = []
  do-vmap (f âˆ· v) = kmap f âˆ· do-vmap v

  enact-laws : Frame Y Z â†’ Frame X Y â†’ Value W X â†’ Value W Z
  push-frm : Frame Y Z â†’ Value X Y â†’ Value X Z

  -- The meat of the solver! This is responsible for enacting the
  -- monad equations (hence the name).
  -- There are 2 important phases to this function: 'kunit' and 'kmult'
  -- floating, and the subsequent elimination of those frames.
  --
  -- When we push a 'kmap' frame, we check to see if the head of the stack
  -- is a 'kunit' or 'kmult'; if so, we float those outwards so that they
  -- always remain at the top of the stack.
  --
  -- Subsequently, when pushing a 'kmult' frame, we need to enact
  -- equations. As the relevant frames are /always/ on the top of the stack,
  -- we can simply apply the relevant equations, and potentially keep pushing
  -- frames down.
  enact-laws (khom f) k' v = khom f âˆ· k' âˆ· v
  enact-laws (kmap k) (kmap k') v = do-vmap (enact-laws k k' []) ++ v
  enact-laws (kmap k) (kunit _) v = kunit _ âˆ· push-frm k v
  enact-laws (kmap k) (kmult _) v = kmult _ âˆ· push-frm (kmap (kmap k)) v
  enact-laws (kunit _) k' v = kunit _ âˆ· k' âˆ· v
  enact-laws (kmult _) (kmap (kmap k')) v = kmult _ âˆ· kmap (kmap k') âˆ· v
  enact-laws (kmult _) (kmap (kunit _)) v = v
  enact-laws (kmult _) (kmap (kmult _)) v = kmult _ âˆ· push-frm (kmult _) v
  enact-laws (kmult _) (kunit _) v = v
  enact-laws (kmult _) (kmult _) v = kmult _ âˆ· kmult _ âˆ· v

  -- Small shim, used to enact a law against a potentially empty stack.
  push-frm k [] = k âˆ· []
  push-frm k (k' âˆ· v) = enact-laws k k' v

  -- Concatenate 2 stacks together, performing simplification via 'enact-laws'.
  do-vcomp : Value Y Z â†’ Value X Y â†’ Value X Z
  do-vcomp [] v2 = v2
  do-vcomp (k âˆ· v1) v2 = push-frm k (do-vcomp v1 v2)

  eval : â€¶Homâ€¶ X Y â†’ Value X Y
  eval (â€¶Mâ‚â€¶ e) = do-vmap (eval e)
  eval (â€¶Î·â€¶ X) = kunit X âˆ· []
  eval (â€¶Î¼â€¶ X) = kmult X âˆ· []
  eval (e1 â€¶âˆ˜â€¶ e2) = do-vcomp (eval e1) (eval e2)
  eval â€¶idâ€¶ = []
  eval (f â†‘) = khom f âˆ· []

  --------------------------------------------------------------------------------
  -- Soundness

  vmap-sound : âˆ€ (v : Value X Y) â†’ âŸ¦ do-vmap v âŸ§áµ¥ â‰¡ Mâ‚ âŸ¦ v âŸ§áµ¥
  vmap-sound [] = sym M-id
  vmap-sound (k âˆ· v) =
    Mâ‚ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ do-vmap v âŸ§áµ¥ â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ vmap-sound v âŸ©
    Mâ‚ âŸ¦ k âŸ§â‚– M.ğ’Ÿ.âˆ˜ Mâ‚ âŸ¦ v âŸ§áµ¥  â‰¡Ë˜âŸ¨ M-âˆ˜ âŸ¦ k âŸ§â‚– âŸ¦ v âŸ§áµ¥ âŸ©
    Mâ‚ (âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥) âˆ

  vconcat-sound : âˆ€ (v1 : Value Y Z) â†’ (v2 : Value X Y) â†’ âŸ¦ v1 ++ v2 âŸ§áµ¥ â‰¡ âŸ¦ v1 âŸ§áµ¥ âˆ˜ âŸ¦ v2 âŸ§áµ¥
  vconcat-sound [] v2 = sym (idl âŸ¦ v2 âŸ§áµ¥)
  vconcat-sound (k âˆ· v1) v2 = pushr (vconcat-sound v1 v2)

  enact-laws-sound : âˆ€ (k1 : Frame Y Z) â†’ (k2 : Frame X Y) â†’ (v : Value W X) â†’ âŸ¦ enact-laws k1 k2 v âŸ§áµ¥ â‰¡ âŸ¦ k1 âŸ§â‚– âˆ˜ âŸ¦ k2 âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥
  push-frm-sound   : âˆ€ (k : Frame Y Z) â†’ (v : Value X Y) â†’ âŸ¦ push-frm k v âŸ§áµ¥ â‰¡ âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥

  enact-laws-sound (khom x) k2 v = refl
  enact-laws-sound (kmap k1) (kmap k2) v =
    âŸ¦ do-vmap (enact-laws k1 k2 []) ++ v âŸ§áµ¥     â‰¡âŸ¨ vconcat-sound (do-vmap (enact-laws k1 k2 [])) v âŸ©
    âŸ¦ do-vmap (enact-laws k1 k2 []) âŸ§áµ¥ âˆ˜ âŸ¦ v âŸ§áµ¥ â‰¡âŸ¨ vmap-sound (enact-laws k1 k2 []) âŸ©âˆ˜âŸ¨refl âŸ©
    Mâ‚ âŸ¦ enact-laws k1 k2 [] âŸ§áµ¥ M.ğ’Ÿ.âˆ˜ âŸ¦ v âŸ§áµ¥    â‰¡âŸ¨ M.pushl (enact-laws-sound k1 k2 []) âŸ©
    Mâ‚ âŸ¦ k1 âŸ§â‚– âˆ˜ Mâ‚ (âŸ¦ k2 âŸ§â‚– âˆ˜ id) âˆ˜ âŸ¦ v âŸ§áµ¥     â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ (M.âŸ¨ idr âŸ¦ k2 âŸ§â‚– âŸ© âŸ©âˆ˜âŸ¨refl) âŸ©
    Mâ‚ âŸ¦ k1 âŸ§â‚– âˆ˜ Mâ‚ âŸ¦ k2 âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥            âˆ
  enact-laws-sound (kmap {Y = Y} k1) (kunit X) v =
    unit.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ push-frm k1 v âŸ§áµ¥    â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ push-frm-sound k1 v âŸ©
    unit.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ k1 âŸ§â‚– âˆ˜ âŸ¦ v âŸ§áµ¥      â‰¡âŸ¨ extendl (unit.is-natural âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’ âŸ¦ k1 âŸ§â‚–) âŸ©
    M.Fâ‚ âŸ¦ k1 âŸ§â‚– âˆ˜ unit.Î· âŸ¦ X âŸ§â‚’ âˆ˜ âŸ¦ v âŸ§áµ¥ âˆ
  enact-laws-sound (kmap {Y = Y} k1) (kmult X) v =
    mult.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ âŸ¦ push-frm (kmap (kmap k1)) v âŸ§áµ¥ â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ push-frm-sound (kmap (kmap k1)) v âŸ©
    mult.Î· âŸ¦ Y âŸ§â‚’ âˆ˜ Mâ‚ (Mâ‚ âŸ¦ k1 âŸ§â‚–) âˆ˜ âŸ¦ v âŸ§áµ¥         â‰¡âŸ¨ extendl (mult.is-natural âŸ¦ X âŸ§â‚’ âŸ¦ Y âŸ§â‚’ âŸ¦ k1 âŸ§â‚–) âŸ©
    M.Fâ‚ âŸ¦ k1 âŸ§â‚– âˆ˜ mult.Î· âŸ¦ X âŸ§â‚’ âˆ˜ âŸ¦ v âŸ§áµ¥            âˆ
  enact-laws-sound (kunit X) k2 v = refl
  enact-laws-sound (kmult X) (kmap (kmap k2)) v = refl
  enact-laws-sound (kmult X) (kmap (kunit .X)) v = insertl left-ident
  enact-laws-sound (kmult X) (kmap (kmult .X)) v =
    mult.Î· âŸ¦ X âŸ§â‚’ âˆ˜ âŸ¦ push-frm (kmult (â€¶Mâ‚€â€¶ X)) v âŸ§áµ¥ â‰¡âŸ¨ reflâŸ©âˆ˜âŸ¨ push-frm-sound (kmult (â€¶Mâ‚€â€¶ X)) v âŸ©
    mult.Î· âŸ¦ X âŸ§â‚’ âˆ˜ mult.Î· (Mâ‚€ âŸ¦ X âŸ§â‚’) âˆ˜ âŸ¦ v âŸ§áµ¥      â‰¡âŸ¨ extendl (sym mult-assoc) âŸ©
    mult.Î· âŸ¦ X âŸ§â‚’ âˆ˜ Mâ‚ (mult.Î· âŸ¦ X âŸ§â‚’) âˆ˜ âŸ¦ v âŸ§áµ¥ âˆ
  enact-laws-sound (kmult X) (kunit _) v = insertl right-ident
  enact-laws-sound (kmult X) (kmult _) v = refl

  push-frm-sound k [] = refl
  push-frm-sound k (k' âˆ· v) = enact-laws-sound k k' v

  vcomp-sound : âˆ€ (v1 : Value Y Z) â†’ (v2 : Value X Y) â†’ âŸ¦ do-vcomp v1 v2 âŸ§áµ¥ â‰¡ âŸ¦ v1 âŸ§áµ¥ âˆ˜ âŸ¦ v2 âŸ§áµ¥
  vcomp-sound [] v2 = sym (idl âŸ¦ v2 âŸ§áµ¥)
  vcomp-sound (k âˆ· v1) v2 =
    âŸ¦ push-frm k (do-vcomp v1 v2) âŸ§áµ¥ â‰¡âŸ¨ push-frm-sound k (do-vcomp v1 v2) âŸ©
    âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ do-vcomp v1 v2 âŸ§áµ¥ â‰¡âŸ¨ pushr (vcomp-sound v1 v2) âŸ©
    (âŸ¦ k âŸ§â‚– âˆ˜ âŸ¦ v1 âŸ§áµ¥) âˆ˜ âŸ¦ v2 âŸ§áµ¥ âˆ

  eval-sound : âˆ€ (e : â€¶Homâ€¶ X Y) â†’ âŸ¦ eval e âŸ§áµ¥ â‰¡ âŸ¦ e âŸ§â‚•
  eval-sound (â€¶Mâ‚â€¶ e) =
    âŸ¦ do-vmap (eval e) âŸ§áµ¥ â‰¡âŸ¨ vmap-sound (eval e) âŸ©
    Mâ‚ âŸ¦ eval e âŸ§áµ¥        â‰¡âŸ¨ M.âŸ¨ eval-sound e âŸ© âŸ©
    Mâ‚ âŸ¦ e âŸ§â‚• âˆ
  eval-sound (â€¶Î·â€¶ X) = idr (unit.Î· âŸ¦ X âŸ§â‚’)
  eval-sound (â€¶Î¼â€¶ X) = idr (mult.Î· âŸ¦ X âŸ§â‚’)
  eval-sound (e1 â€¶âˆ˜â€¶ e2) =
    âŸ¦ do-vcomp (eval e1) (eval e2) âŸ§áµ¥ â‰¡âŸ¨ vcomp-sound (eval e1) (eval e2) âŸ©
    âŸ¦ eval e1 âŸ§áµ¥ âˆ˜ âŸ¦ eval e2 âŸ§áµ¥       â‰¡âŸ¨ apâ‚‚ _âˆ˜_ (eval-sound e1) (eval-sound e2) âŸ©
    âŸ¦ e1 âŸ§â‚• âˆ˜ âŸ¦ e2 âŸ§â‚•                 âˆ
  eval-sound â€¶idâ€¶ = refl
  eval-sound (f â†‘) = idr f

  abstract
    solve : âˆ€ (e1 e2 : â€¶Homâ€¶ X Y) â†’ âŸ¦ eval e1 âŸ§áµ¥ â‰¡ âŸ¦ eval e2 âŸ§áµ¥ â†’ âŸ¦ e1 âŸ§â‚• â‰¡ âŸ¦ e2 âŸ§â‚•
    solve e1 e2 p = sym (eval-sound e1) Â·Â· p Â·Â· (eval-sound e2)

module Reflection where

  pattern category-args xs =
    _ hmâˆ· _ hmâˆ· _ vâˆ· xs

  pattern functor-args functor xs =
    _ hmâˆ· _ hmâˆ· _ hmâˆ· _ hmâˆ· _ hmâˆ· _ hmâˆ· functor vâˆ· xs

  pattern nat-trans-args nt args =
    _ hmâˆ· _ hmâˆ· _ hmâˆ· _ hmâˆ·
    _ hmâˆ· _ hmâˆ·
    _ hmâˆ· _ hmâˆ·
    nt vâˆ· args


  pattern monad-args monad xs =
    _ hmâˆ· _ hmâˆ· _ hmâˆ· monad vâˆ· xs

  pattern monad-fn-args monad xs =
    _ hâˆ· _ hâˆ· _ hâˆ· monad vâˆ· xs

  pattern â€œidâ€ =
    def (quote Precategory.id) (category-args (_ hâˆ· []))

  pattern â€œâˆ˜â€ f g =
    def (quote Precategory._âˆ˜_) (category-args (_ hâˆ· _ hâˆ· _ hâˆ· f vâˆ· g vâˆ· []))

  pattern â€œMâ‚€â€ monad x =
    def (quote Monad.Mâ‚€) (monad-fn-args monad (x vâˆ· []))

  pattern â€œMâ‚â€ monad f =
    def (quote Monad.Mâ‚) (monad-fn-args monad (_ hâˆ· _ hâˆ· f vâˆ· []))

  pattern â€œÎ·â€ monad x =
    def (quote _=>_.Î·) (nat-trans-args (def (quote Monad.unit) (monad-args monad [])) (x vâˆ· []))

  pattern â€œÎ¼â€ monad x =
    def (quote _=>_.Î·) (nat-trans-args (def (quote Monad.mult) (monad-args monad [])) (x vâˆ· []))

  mk-monad-args : Term â†’ List (Arg Term) â†’ List (Arg Term)
  mk-monad-args monad args = unknown hâˆ· unknown hâˆ· unknown hâˆ· monad vâˆ· args

  â€œsolveâ€ : Term â†’ Term â†’ Term â†’ Term
  â€œsolveâ€ monad lhs rhs =
    def (quote NbE.solve) (mk-monad-args monad $ infer-hidden 2 $ lhs vâˆ· rhs vâˆ· def (quote refl) [] vâˆ· [])

  build-object-expr : Term â†’ Term â†’ TC Term
  build-object-expr monad (â€œMâ‚€â€ monad' x) = do
    unify monad monad'
    x â† build-object-expr monad x
    pure $ con (quote NbE.â€¶Mâ‚€â€¶) (x vâˆ· [])
  build-object-expr monad x =
    pure $ con (quote NbE.â€¶_â€¶) (x vâˆ· [])

  build-hom-expr : Term â†’ Term â†’ TC Term
  build-hom-expr monad â€œidâ€ =
    pure $ con (quote NbE.â€¶idâ€¶) []
  build-hom-expr monad (â€œâˆ˜â€ f g) = do
    f â† build-hom-expr monad f
    g â† build-hom-expr monad g
    pure $ con (quote NbE._â€¶âˆ˜â€¶_) (f vâˆ· g vâˆ· [])
  build-hom-expr monad (â€œMâ‚â€ monad' f) = do
    unify monad monad'
    f â† build-hom-expr monad f
    pure $ con (quote NbE.â€¶Mâ‚â€¶) (f vâˆ· [])
  build-hom-expr monad (â€œÎ·â€ monad' x) = do
    unify monad monad'
    x â† build-object-expr monad x
    pure $ con (quote NbE.â€¶Î·â€¶) (x vâˆ· [])
  build-hom-expr monad (â€œÎ¼â€ monad' x) = do
    x â† build-object-expr monad x
    unify monad monad'
    pure $ con (quote NbE.â€¶Î¼â€¶) (x vâˆ· [])
  build-hom-expr monad f =
    pure $ con (quote NbE._â†‘) (f vâˆ· [])

  dont-reduce : List Name
  dont-reduce =
    quote Precategory.id âˆ· quote Precategory._âˆ˜_ âˆ·
    quote Functor.Fâ‚€ âˆ· quote Functor.Fâ‚ âˆ·
    quote _=>_.Î· âˆ·
    quote Monad.M âˆ· quote Monad.unit âˆ· quote Monad.mult âˆ· []

  solve-macro : âˆ€ {o h} {ğ’ : Precategory o h} â†’ Monad ğ’ â†’ Term â†’ TC âŠ¤
  solve-macro monad hole =
    withNormalisation false $
    withReduceDefs (false , dont-reduce) $ do
      monad-tm â† quoteTC monad
      goal â† infer-type hole >>= reduce
      just (lhs , rhs) â† get-boundary goal
        where nothing â†’ typeError $ strErr "Can't determine boundary: " âˆ·
                                    termErr goal âˆ· []
      elhs â† build-hom-expr monad-tm lhs
      erhs â† build-hom-expr monad-tm rhs
      noConstraints $ unify hole (â€œsolveâ€ monad-tm elhs erhs)

macro
  monad! : âˆ€ {o h} {ğ’ : Precategory o h} â†’ Monad ğ’ â†’ Term â†’ TC âŠ¤
  monad! monad = Reflection.solve-macro monad

private module Test {o h} {ğ’ : Precategory o h} (monad : Monad ğ’) where
  open Precategory ğ’
  open Monad monad

  variable
    A B C : Ob

  test : âˆ€ {f : Hom B C} {g : Hom A B}
         â†’ mult.Î· C âˆ˜ Mâ‚ (Mâ‚ (f âˆ˜ g)) âˆ˜ unit.Î· (Mâ‚€ A) â‰¡ Mâ‚ f âˆ˜ Mâ‚ (id âˆ˜ g)
  test = monad! monad

  test-assoc : âˆ€ X â†’ mult.Î· X âˆ˜ Mâ‚ (mult.Î· X) â‰¡ mult.Î· X âˆ˜ mult.Î· (Mâ‚€ X)
  test-assoc X = monad! monad

  test-nested : âˆ€ X â†’ Mâ‚ (mult.Î· X âˆ˜ unit.Î· (Mâ‚€ X)) â‰¡ id
  test-nested _ = monad! monad

  test-separate : âˆ€ X â†’ Mâ‚ (mult.Î· X) âˆ˜ Mâ‚ (unit.Î· (Mâ‚€ X)) â‰¡ id
  test-separate _ = monad! monad
