<!--
```agda
open import 1Lab.Reflection.HLevel

open import Cat.Functor.Adjoint
open import Cat.Prelude

open import Data.Partial.Total
open import Data.Partial.Base

open import Realisability.PCA.Trivial
open import Realisability.PCA

import 1Lab.Reflection as R

import Cat.Reasoning as Cat

import Realisability.Data.Bool
import Realisability.PCA.Sugar
import Realisability.Base

open R hiding (def ; absurd)
open Functor
open _=>_
open _âŠ£_
```
-->

```agda
module Cat.Instances.Assemblies where
```

<!--
```agda
private variable
  â„“ â„“' â„“A : Level
  ğ”¸ : PCA â„“A
```
-->

# Assemblies over a PCA

When working over a [[partial combinatory algebra]] $\bA$, it's often
the case that we're interested in programs $\tt{p} : \bA$ as concrete
*implementations* of some mathematical datum $x : X$. For example, we
can implement the successor function on natural numbers to be
$$
\tt{suc} = \langle n \rangle \langle f \rangle \langle x \rangle\ f(nfx)
$$,
representing a numeral $n : \bN$ as a *Church numeral*, taking the
defining property of $\operatorname{suc} n$ to be that if we have some
iterable process $f : A \to A$ starting at $x : A$, then the
$(\operatorname{suc} n)$-th iteration is $f$ applied to the $n$th
iteration; But we could just as well implement
$$
\tt{suc} = \langle n \rangle\ \tt{pair}(\tt{false}, n)
$$
representing a numeral $n : \bN$ as a *Curry numeral*, a pair containing
the information of whether the number is zero and its predecessor (if
any). These implementations are extensionally identical, in that they
both denote the same actual natural number, but for a concrete pca $\bA$,
they might genuinely be different --- we could imagine measuring the
time complexity of the predecessor function, which is $O(1)$ for Curry
numbers and $O(n)$ for Church numbers. Therefore, if we are to
investigate the computational content of constructive mathematics, we
need a way to track the connection between the mathematical elements $x
: X$ and the programs $\tt{p} : \bA$ which denote them.

:::{.definition #assembly}
An **assembly** over a pca $\bA$ is a [[set]] $X$ equipped with a
[[propositional|proposition]] relation $\tt{p} \Vdash x$ between
programs $\tt{p} : \bA$ and elements $x : X$; when this holds, we say
$\tt{p}$ **realises** $x$. Moreover, for every $x : X$, we require that
there be at least one $\tt{p}$ which realises it.
:::

::: warning
The construction of assemblies over $\bA$, and the category
$\thecat{Asm}(\bA)$, works regardless of *which* pca $\bA$ we choose ---
but we only get something *interesting* if $\bA$ is [[nontrivial|trivial
pca]]: the category $\thecat{Asm}(*)$ over the trivial pca is the
category $\Sets$.

Therefore, when making natural-language statements about
$\thecat{Asm}(\bA)$, we generally assume that $\bA$ is nontrivial. A
statement like "the category $\thecat{Asm}(\bA)$ is not
[[univalent|univalent category]]" should be read as saying "univalence
of $\thecat{Asm}(\bA)$ implies $\bA$ is trivial."
:::

A prototypical example is the assembly of booleans, `ğŸš`{.Agda}, defined
[below](#the-assembly-of-booleans). Its set of elements is
`Bool`{.Agda}, and we fix realisers
$$
\begin{align*}
\left(\langle x \rangle \langle y \rangle\ x\right) \Vdash&\ \rm{true}\\
\left(\langle x \rangle \langle y \rangle\ y\right) \Vdash&\ \rm{false;}
\end{align*}
$$
see [[booleans in a pca]] for the details of the construction. This is not
the only possible choice: we could, for example, invert the realisers,
and say that the value `true`{.Agda} is implemented by the *program*
$\tt{false}$ (and vice-versa). This results in a genuinely different
assembly over `Bool`{.Agda}, though with the same denotational data.

```agda
record Assembly (ğ”¸ : PCA â„“A) â„“ : Type (lsuc â„“ âŠ” â„“A) where
  no-eta-equality
  field
    Ob         : Type â„“
    has-is-set : is-set Ob
    realisers  : Ob â†’ â„™âº ğ”¸
    realised   : âˆ€ x â†’ âˆƒ[ a âˆˆ â†¯ âŒ ğ”¸ âŒŸ ] (a âˆˆ realisers x)
```

<!--
```agda
  module _ {x : Ob} where open â„™âº (realisers x) using (defined) public

open Assembly public

private variable
  X Y Z : Assembly ğ”¸ â„“

instance
  Underlying-Assembly : Underlying (Assembly ğ”¸ â„“)
  Underlying-Assembly = record { âŒ_âŒŸ = Assembly.Ob }

  hlevel-proj-asm : hlevel-projection (quote Assembly.Ob)
  hlevel-proj-asm .hlevel-projection.has-level = quote Assembly.has-is-set
  hlevel-proj-asm .hlevel-projection.get-level _ = pure (quoteTerm (suc (suc zero)))
  hlevel-proj-asm .hlevel-projection.get-argument (_ âˆ· _ âˆ· _ âˆ· c vâˆ· []) = pure c
  hlevel-proj-asm .hlevel-projection.get-argument (_ âˆ· c vâˆ· []) = pure c
  {-# CATCHALL #-}
  hlevel-proj-asm .hlevel-projection.get-argument _ = typeError []

module _ (X : Assembly ğ”¸ â„“) (a : â†¯ âŒ ğ”¸ âŒŸ) (x : âŒ X âŒŸ) where
  open Î© (X .realisers x .mem a) renaming (âˆ£_âˆ£ to [_]_âŠ©_) public

-- This module can't be parametrised so this display form can fire
-- (otherwise it gets closed over pattern variables that aren't solvable
-- from looking at the expression, like the level and the PCA):
{-# DISPLAY realisers X x .â„™âº.mem a = [ X ] a âŠ© x #-}

substâŠ©
  : {ğ”¸ : PCA â„“A} (X : Assembly ğ”¸ â„“) {x : âŒ X âŒŸ} {p q : â†¯ âŒ ğ”¸ âŒŸ}
  â†’ [ X ] p âŠ© x â†’ q â‰¡ p â†’ [ X ] q âŠ© x
substâŠ© X {x} hx p = subst (_âˆˆ X .realisers x) (sym p) hx
```
-->

To understand the difference --- and similarity --- between the ordinary
assembly of booleans and the swapped booleans, we define a morphism of
assemblies $(X, \Vdash_X) \to (Y, \Vdash_Y)$ to be a function $f : X \to
Y$ satisfying the [[*property*|propositional truncation]] that there
exists a program $\tt{f} : \bA$ which sends realisers of $x : X$ to
realisers of $f(x) : Y$.

Note the force of the propositional truncation in this definition: maps
of assemblies are identical *when they have the same underlying
function*, regardless of which programs potentially implement them.
Since we can not, for a general $\bA$, show that the programs
$\mathtt{f}$ and
$$
\langle a \rangle\ f\ a
$$
are identical, $\thecat{Asm}(\bA)$ would not be a category if the choice
of realiser mattered for identity of computable maps.

```agda
record Assembly-hom {ğ”¸ : PCA â„“A} (X : Assembly ğ”¸ â„“) (Y : Assembly ğ”¸ â„“') : Type (â„“A âŠ” â„“ âŠ” â„“') where
  open Realisability.Base ğ”¸ using ([_]_âŠ¢_)

  field
    map     : âŒ X âŒŸ â†’ âŒ Y âŒŸ
    tracked : âˆ¥ [ map ] X .realisers âŠ¢ Y .realisers âˆ¥
```

<!--
```agda
private unquoteDecl eqv = declare-record-iso eqv (quote Assembly-hom)

instance
  H-Level-Assembly-hom : âˆ€ {n} â†’ H-Level (Assembly-hom X Y) (2 + n)
  H-Level-Assembly-hom = basic-instance 2 $ Isoâ†’is-hlevel 2 eqv (hlevel 2)

  Extensional-Assembly-hom
    : âˆ€ {â„“r} â¦ƒ _ : Extensional (âŒ X âŒŸ â†’ âŒ Y âŒŸ) â„“r â¦„
    â†’ Extensional (Assembly-hom X Y) â„“r
  Extensional-Assembly-hom â¦ƒ e â¦„ = injectionâ†’extensional! (Î» p â†’ Iso.injective eqv (Î£-prop-path! p)) e

  Funlike-Assembly-hom : Funlike (Assembly-hom X Y) âŒ X âŒŸ Î» _ â†’ âŒ Y âŒŸ
  Funlike-Assembly-hom = record { _Â·_ = Assembly-hom.map }

{-# DISPLAY Assembly-hom.map f x = f Â· x #-}

-- Helper record for constructing an assembly map when the realiser is
-- known/does not depend on other truncated data; the 'tracks' field has
-- all visible arguments to work with `record where` syntax.

record make-assembly-hom {ğ”¸ : PCA â„“A} (X : Assembly ğ”¸ â„“) (Y : Assembly ğ”¸ â„“') : Type (â„“A âŠ” â„“ âŠ” â„“') where
  open Realisability.PCA.Sugar ğ”¸ using (_â‹†_)
  field
    map      : âŒ X âŒŸ â†’ âŒ Y âŒŸ
    realiser : â†¯âº ğ”¸
    tracks   : (x : âŒ X âŒŸ) (a : â†¯ âŒ ğ”¸ âŒŸ) (ah : [ X ] a âŠ© x) â†’ [ Y ] realiser â‹† a âŠ© map x

open Assembly-hom public

to-assembly-hom
  : âˆ€ {ğ”¸ : PCA â„“A} {X : Assembly ğ”¸ â„“} {Y : Assembly ğ”¸ â„“'}
  â†’ make-assembly-hom X Y
  â†’ Assembly-hom X Y
{-# INLINE to-assembly-hom #-}

to-assembly-hom f = record { make-assembly-hom f using (map) ; tracked = inc record { make-assembly-hom f } }

module _ (ğ”¸ : PCA â„“A) where
  open Realisability.Base ğ”¸
  open Realisability.PCA.Sugar ğ”¸
  open Realisability.Data.Bool ğ”¸

  open Assembly-hom
  open Precategory
```
-->

This consideration is necessary for assemblies and assembly morphisms to
be a category: in an arbitrary pca $\bA$, composition of programs need
not be unital or associative.

```agda
  Assemblies : âˆ€ â„“ â†’ Precategory (lsuc â„“ âŠ” â„“A) (â„“A âŠ” â„“)
  Assemblies â„“ .Ob      = Assembly ğ”¸ â„“
  Assemblies â„“ .Hom     = Assembly-hom
  Assemblies â„“ .Hom-set x y = hlevel 2
  Assemblies â„“ .id      = record where
    map x   = x
    tracked = inc idâŠ¢
  Assemblies â„“ ._âˆ˜_ f g = record where
    map x   = f Â· (g Â· x)
    tracked = â¦‡ f .tracked âˆ˜âŠ¢ g .tracked â¦ˆ
  Assemblies â„“ .idr   f     = ext Î» _ â†’ refl
  Assemblies â„“ .idl   f     = ext Î» _ â†’ refl
  Assemblies â„“ .assoc f g h = ext Î» _ â†’ refl
```

::: warning
Unlike most other categories constructed on the 1Lab, the category of
assemblies is not [[univalent|univalent category]]; see [univalence of
categories of assemblies](Cat.Instances.Assemblies.Univalence.html).[^univalence]
:::

[^univalence]:
    This is essentially *because* of of assemblies such as `ğŸš`{.Agda}
    and its "flipped" counterpart, described above. The identity map is
    a computable isomorphism between them, realised by the `` `not
    ``{.Agda} program, which does not extend to a path (unless $\bA$ is
    trivial).

However, these two assemblies *are* still identical in the type
`Assembly`{.Agda}, where we allow the identification between the sets to
be nontrivial --- their realisability relations are identical over the
`not`{.Agda} equivalence --- hence the comment above about these being
non-trivial assemblies "over bool".

<!--
```agda
  _ = not
  _ = `not
```
-->

## The assembly of booleans

The assembly of booleans, $\tt{2}$, is the simplest example of an
assembly which contains actual computability data. Its construction is
entirely straightforward:

```agda
  ğŸš : Assembly ğ”¸ lzero
  ğŸš .Ob          = Bool
  ğŸš .has-is-set  = hlevel 2
  ğŸš .realisers true  = singletonâº `true
  ğŸš .realisers false = singletonâº `false
  ğŸš .realised  true  = inc (`true .fst , inc refl)
  ğŸš .realised  false = inc (`false .fst , inc refl)
```

We define the realisability relation as a function from `Bool`{.Agda},
by cases: the only option for realising the boolean `true`{.Agda} is
with the `` `true ``{.Agda} program, and similarly the `false`{.Agda}
boolean is realised by the `` `false ``{.Agda} program. Both elements
have those respective programs as their realisers.

## Indiscrete assemblies

However, the *assembly* of booleans is not the only assembly we can
construct on the *type* of booleans. As mentioned above, we could also
have inverted which program realises each boolean; but this is *still*
an assembly with nontrivial computability data. Now, we show that the
"ambient" world of sets and functions embeds [[fully faithful|fully
faithful functor]] into the category of assemblies over any pca $\bA$.

This is, perhaps, a bit surprising: maps of assemblies are computable by
definition, but arbitrary functions between sets need not be! The catch
is that, when equipping a set with the structure of an assembly, *we*
get to choose which programs compute each elements; and, above, we have
made a sensible choice. But we can always make an *adversarial* choice,
letting *every* program at all realise any element.

::: terminology
We denote the **indiscrete assembly** on a set $X$ as $\nabla X$,
following the literature. Note however that Bauer
[-@Bauer:Realisability] refers to these as *constant assemblies*, while
de Jong [-@deJong:Realisability] does not assign them a name but merely
singles them out as embedding classical logic in $\thecat{Asm}(\bA)$.
:::

```agda
  âˆ‡ : âˆ€ {â„“} (X : Type â„“) â¦ƒ _ : H-Level X 2 â¦„ â†’ Assembly ğ”¸ â„“
  âˆ‡ X .Ob          = X
  âˆ‡ X .has-is-set  = hlevel 2
  âˆ‡ X .realisers x = record
    { mem     = def
    ; defined = Î» x â†’ x
    }
  âˆ‡ X .realised x = inc (expr âŸ¨ x âŸ© x , absâ†“ _ _)
```

The important thing to know about these is that any function of sets $X
\to Y$ extends to a computable map of assemblies $(X, \Vdash) \to \nabla
Y$ --- this is because the only requirement for $e \Vdash_{\nabla Y} f
x$ is that $e$ is defined, and assemblies are defined so that if $e
\Vdash_X x$ then $e$ is defined.

<details>
<summary>Following the general logic of [[adjoint functors]], this means
that $\nabla (-)$ is a functor $\Sets \to \thecat{Asm}(\bA)$, for any
$\bA$ at all --- and moreover that $\nabla$ is a [[right adjoint]] to
the functor $\Gamma : \thecat{Asm}(\bA) \to \Sets$ which projects the
underlying set of each assembly.</summary>

```agda
  Cofree : Functor (Sets â„“) (Assemblies â„“)
  Cofree .Fâ‚€ X = âˆ‡ âŒ X âŒŸ
  Cofree .Fâ‚ f = to-assembly-hom record where
    map           = f
    realiser      = val âŸ¨ x âŸ© x
    tracks x a ha = subst âŒ_âŒŸ (sym (abs-Î² _ [] (a , ha))) ha
  Cofree .F-id    = ext Î» _ â†’ refl
  Cofree .F-âˆ˜ f g = ext Î» _ â†’ refl

  Forget : Functor (Assemblies â„“) (Sets â„“)
  Forget .Fâ‚€ X    = el! âŒ X âŒŸ
  Forget .Fâ‚ f    = f Â·_
  Forget .F-id    = refl
  Forget .F-âˆ˜ f g = refl

  ForgetâŠ£âˆ‡ : Forget {â„“} âŠ£ Cofree
  ForgetâŠ£âˆ‡ .unit .Î· X = to-assembly-hom record where
    map x         = x
    realiser      = val âŸ¨ x âŸ© x
    tracks x a ha = subst âŒ_âŒŸ (sym (abs-Î² _ [] (a , X .defined ha))) (X .defined ha)

  ForgetâŠ£âˆ‡ .unit .is-natural x y f = ext Î» _ â†’ refl
  ForgetâŠ£âˆ‡ .counit .Î· X a = a
  ForgetâŠ£âˆ‡ .counit .is-natural x y f = refl
  ForgetâŠ£âˆ‡ .zig = refl
  ForgetâŠ£âˆ‡ .zag = ext Î» _ â†’ refl
```

</details>

The indiscrete assemblies $\nabla X$ are generally poor as *domains* for
computable functions, since a realiser for $f : \nabla X \to (Y,
\Vdash)$ would have to choose realisers for $f(x)$ given no information
about $x$. Indeed, we can show that if there are non-constant maps
$\nabla \{0, 1\} \to \tt{2}$, then $\bA$ is [[trivial|trivial pca]].

```agda
  non-constant-nabla-map
    : (f : Assembly-hom (âˆ‡ Bool) ğŸš)
    â†’ f Â· true â‰  f Â· false
    â†’ is-trivial-pca ğ”¸
  non-constant-nabla-map f x = case f .tracked of Î» where
    record { realiser = (fp , fâ†“) ; tracks = t } â†’
      let
        a = t true  (`true .fst) (`true .snd)
        b = t false (`true .fst) (`true .snd)

        cases
          : âˆ€ b b' (x : â†¯ âŒ ğ”¸ âŒŸ)
          â†’ [ ğŸš ] x âŠ© b â†’ [ ğŸš ] x âŠ© b'
          â†’ b â‰  b' â†’ `true .fst â‰¡ `false .fst
        cases = Î» where
          true true   p â†’ rec! Î» rb rb' tâ‰ t â†’ absurd (tâ‰ t refl)
          true false  p â†’ rec! Î» rb rb' _   â†’ rb âˆ™ sym rb'
          false true  p â†’ rec! Î» rb rb' _   â†’ rb' âˆ™ sym rb
          false false p â†’ rec! Î» rb rb' fâ‰ f â†’ absurd (fâ‰ f refl)
      in cases (f Â· true) (f Â· false) _ a b x
```

<!--
```agda
module Asm {â„“A â„“} {ğ”¸ : PCA â„“A} = Cat (Assemblies ğ”¸ â„“)
```
-->
