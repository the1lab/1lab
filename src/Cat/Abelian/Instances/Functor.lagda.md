<!--
```agda
open import Algebra.Group.Ab
open import Algebra.Monoid
open import Algebra.Group

open import Cat.Abelian.Instances.Ab
open import Cat.Instances.Functor
open import Cat.Abelian.Functor
open import Cat.Abelian.Base
open import Cat.Prelude
```
-->

```agda
module Cat.Abelian.Instances.Functor
  where
```

<!--
```agda
module _
  {o o' â„“ â„“'} {A : Precategory o â„“}   (ğ’œ : Ab-category A)
              {B : Precategory o' â„“'} (â„¬ : Ab-category B)
  where
  private
    module A = Ab-category ğ’œ
    module B = Ab-category â„¬
  open Precategory
  open Ab-category
  open Ab-functor
  open _=>_
```
-->

# Ab-enriched functor categories

Recall that, for a pair of [$\Ab$-categories] $\cA$ and $\cB$, we
define the [$\Ab$-functors] between them to be the functors $F : \cA
\to \cB$ which additionally preserve homwise addition[^pres-add].
We can, mimicking the construction of the ordinary [functor category],
construct a category $[\cA, \cB]_{\Ab}$ consisting only of the
$\Ab$-functors, and prove that it is _also_ an $\Ab$-category.

[^pres-add]: i.e. those functors $F$ for which, for all $a, b$, the
$\hom$-mapping $F_{a,b} : \cA(a,b) \to \cB(F(a),F(b))$ extends to
a group homomorphism.

[$\Ab$-categories]: Cat.Abelian.Base.html#ab-enriched-categories
[$\Ab$-functors]: Cat.Abelian.Functor.html#ab-enriched-functors
[functor category]: Cat.Functor.Base.html

```agda
  Ab-functors : Precategory _ _
  Ab-functors .Ob          = Ab-functor ğ’œ â„¬
  Ab-functors .Hom F G     = F .functor => G .functor
  Ab-functors .Hom-set _ _ = Nat-is-set
  Ab-functors .id    = Cat[ A , B ] .Precategory.id
  Ab-functors ._âˆ˜_   = Cat[ A , B ] .Precategory._âˆ˜_
  Ab-functors .idr   = Cat[ A , B ] .Precategory.idr
  Ab-functors .idl   = Cat[ A , B ] .Precategory.idl
  Ab-functors .assoc = Cat[ A , B ] .Precategory.assoc
```

We can calculate that the natural transformations $F \To G$ between
$\Ab$-functors have a pointwise [[abelian group]] structure. The most
important thing to verify is that the pointwise sum of natural
transformations is natural, which follows from multilinearity of the
composition operation.

```agda
  [_,_]Ab : Ab-category Ab-functors
  [_,_]Ab .Abelian-group-on-hom F G = to-abelian-group-on grp where
    open make-abelian-group
    open Group-on
    module F = Ab-functor F
    module G = Ab-functor G

    grp : make-abelian-group (F .functor => G .functor)
    grp .mul f g .Î· x = f .Î· x B.+ g .Î· x
    grp .mul f g .is-natural x y h =
      (f .Î· y B.+ g .Î· y) B.âˆ˜ F.â‚ h             â‰¡Ë˜âŸ¨ B.âˆ˜-linear-l _ _ _ âŸ©
      (f .Î· y B.âˆ˜ F.â‚ h) B.+ (g .Î· y B.âˆ˜ F.â‚ h) â‰¡âŸ¨ apâ‚‚ B._+_ (f .is-natural x y h) (g .is-natural x y h) âŸ©
      (G.â‚ h B.âˆ˜ f .Î· x) B.+ (G.â‚ h B.âˆ˜ g .Î· x) â‰¡âŸ¨ B.âˆ˜-linear-r _ _ _ âŸ©
      G.â‚ h B.âˆ˜ (f .Î· x B.+ g .Î· x)             âˆ
```

Specifically, given $(\eta_x + \eps_x)F(h)$, we can distribute $F(h)$
into the composite, apply naturality to both summands, and distribute
$G(h)$ _out_ of the composite on the left. Similar computations
establish that the pointwise inverse of natural transformations is
natural.

```agda
    grp .1g .Î· x = B.0m
    grp .1g .is-natural x y f = B.âˆ˜-zero-l âˆ™ sym (B.âˆ˜-zero-r)

    grp .inv f .Î· x = B.Hom.inverse (f .Î· x)
    grp .inv f .is-natural x y g =
      B.Hom.inverse (f .Î· y) B.âˆ˜ F.â‚ g   â‰¡Ë˜âŸ¨ B.neg-âˆ˜-l âŸ©
      B.Hom.inverse âŒœ f .Î· y B.âˆ˜ F.â‚ g âŒ â‰¡âŸ¨ ap! (f .is-natural x y g) âŸ©
      B.Hom.inverse (G.â‚ g B.âˆ˜ f .Î· x)   â‰¡âŸ¨ B.neg-âˆ˜-r âŸ©
      G.â‚ g B.âˆ˜ B.Hom.inverse (f .Î· x)   âˆ

    grp .assoc _ _ _ = Nat-path Î» _ â†’ B.Hom.associative
    grp .idl _       = Nat-path Î» x â†’ B.Hom.idl
    grp .invl _      = Nat-path Î» x â†’ B.Hom.inversel
    grp .comm _ _    = Nat-path Î» x â†’ B.Hom.commutes
    grp .ab-is-set   = Nat-is-set

  [_,_]Ab .âˆ˜-linear-l f g h = Nat-path Î» x â†’ B.âˆ˜-linear-l _ _ _
  [_,_]Ab .âˆ˜-linear-r f g h = Nat-path Î» x â†’ B.âˆ˜-linear-r _ _ _
```
