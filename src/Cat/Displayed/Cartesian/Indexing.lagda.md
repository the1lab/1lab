<!--
```agda
open import Cat.Bi.Instances.Discrete
open import Cat.Displayed.Cartesian
open import Cat.Instances.Discrete
open import Cat.Instances.Functor
open import Cat.Displayed.Fibre
open import Cat.Displayed.Base
open import Cat.Bi.Base
open import Cat.Prelude

import Cat.Displayed.Fibre.Reasoning
import Cat.Displayed.Reasoning
import Cat.Reasoning
import Cat.Morphism as Mor

open Pseudofunctor
open Lax-functor
open _=>_
```
-->

```agda
module Cat.Displayed.Cartesian.Indexing
  {o â„“ o' â„“'} {B : Precategory o â„“}
  (E : Displayed B o' â„“')
  (cartesian : Cartesian-fibration E)
  where
```

<!--
```agda
open Cat.Displayed.Reasoning E
open Cartesian-fibration E cartesian
open Cat.Reasoning B
open Cartesian-lift
open is-cartesian
open Functor

private module Fib = Cat.Displayed.Fibre.Reasoning E
```
-->

# Reindexing for cartesian fibrations {defines="base-change"}

A [[cartesian fibration]] can be thought of as a [[displayed category]]
$\cE$ whose [[fibre categories]] $\cE^*(b)$ depend
([[pseudo|pseudofunctor]])functorially
on the object $b : \cB$ from the base category. A canonical example is
the [[canonical self-indexing]]: If $\cC$ is a
category with [[pullbacks]], then each $b \xto{f} a : \cC$ gives rise to
[[a functor|pullback functor]] $\cC/a \to \cC/b$, the _change of base_
along $f$.

```agda
module _ {ğ’¶ ğ’·} (f : Hom ğ’¶ ğ’·) where
  base-change : Functor (Fibre E ğ’·) (Fibre E ğ’¶)
  base-change .Fâ‚€ ob = f ^* ob
  base-change .Fâ‚ {x} {y} vert = rebase f vert
```

<!--
```agda
  base-change .F-id {x} =
    sym $ Ï€*.uniquep _ _ _ _ $
      idr' _ âˆ™[] symP (idl' _)

  base-change .F-âˆ˜ {x} {y} {z} f' g' =
    sym $ Ï€*.uniquep _ _ _ _ $
      Fib.pulllf (Ï€*.commutesp id-comm _)
      âˆ™[] pullr[] _ (Ï€*.commutesp id-comm _)
      âˆ™[] pulll[] _ Fib.to-fibre
```
-->

Moreover, this assignment is _itself_ functorial in $f$: Along the
identity morphism, it's the same thing as not changing bases at all.

```agda
module _ {ğ’¶} where
  private
    module FC = Cat.Reasoning (Cat[ Fibre E ğ’¶ , Fibre E ğ’¶ ])
    module Fa = Cat.Reasoning (Fibre E ğ’¶)

  base-change-id : base-change id FC.â‰… Id
```

<details>
<summary> I'll warn you in advance that this proof is not for the faint
of heart. </summary>
```agda
  base-change-id = to-natural-iso mi where
    open make-natural-iso
    mi : make-natural-iso (base-change id) Id
    mi .eta x = Ï€* id x
    mi .inv x = Ï€*.universalv id'
    mi .etaâˆ˜inv x = cancel _ _ (Ï€*.commutesv _)
    mi .invâˆ˜eta x = sym $ Ï€*.uniquepâ‚‚ _ _ _ _ _
      (idr' _)
      (Fib.cancellf (Ï€*.commutesv _))
    mi .natural x y f =
      sym $ from-pathp[] $ cast[] $
        Ï€*.commutesp id-comm _
        âˆ™[] Fib.to-fibre
```
</details>

And similarly, composing changes of base is the same thing as changing
base along a composite.

<!--
```agda
  ^*-id-to : âˆ€ {x} â†’ Hom[ id {ğ’¶} ] (id ^* x) x
  ^*-id-to = Ï€* _ _

  ^*-id-from : âˆ€ {x} â†’ Hom[ id {ğ’¶} ] x (id ^* x)
  ^*-id-from = Ï€*.universalv id'

^*-comp-from
  : âˆ€ {a b c} {z} {f : Hom b c} {g : Hom a b}
  â†’ Hom[ id ] (g ^* (f ^* z)) ((f âˆ˜ g) ^* z)
^*-comp-from {z = z} {f = f} {g = g} =
  Ï€*.universalv (Ï€* f z âˆ˜' Ï€* g (f ^* z))

^*-comp-to
  : âˆ€ {a b c} {z} {f : Hom b c} {g : Hom a b}
  â†’ Hom[ id ] ((f âˆ˜ g) ^* z) (g ^* (f ^* z))
^*-comp-to {z = z} {f = f} {g = g} = Ï€*.universalv (Ï€*.universal g (Ï€* (f âˆ˜ g) z))

^*-comp
  : âˆ€ {a b c} {z} {f : Hom b c} {g : Hom a b}
  â†’ ((f âˆ˜ g) ^* z) Fib.â‰… (g ^* (f ^* z))
^*-comp = Fib.make-iso ^*-comp-to ^*-comp-from
  (Ï€*.uniquepâ‚‚ _ _ _ _ _
    (Fib.pulllf (Ï€*.commutesv _) âˆ™[]
      Ï€*.uniquepâ‚‚ _ (idr _) refl _ _
        (pulll[] _ (Ï€*.commutes _ _) âˆ™[]
          Ï€*.commutesv _) refl)
    (idr' _))
  (Ï€*.uniquepâ‚‚ _ _ _ _ _
    (Fib.pulllf (Ï€*.commutesv _)
      âˆ™[] pullr[] _ (Ï€*.commutesv _)
      âˆ™[] Ï€*.commutes _ _)
    (idr' _))

^*-comp-to-natural
  : âˆ€ {a b c} {f : Hom b c} {g : Hom a b} {x y : Ob[ c ]} (f' : Hom[ id ] x y)
  â†’ rebase g (rebase f f') Fib.âˆ˜ ^*-comp-to â‰¡ ^*-comp-to Fib.âˆ˜ rebase (f âˆ˜ g) f'
^*-comp-to-natural {f = f} {g = g} f' =
  ap hom[] $ cartesianâ†’weak-monic E (Ï€*.cartesian) _ _ _ $ cast[] $
    pulll[] _ (Ï€*.commutesp id-comm _)
    âˆ™[] pullr[] _ (Ï€*.commutesv _)
    âˆ™[] Ï€*.uniquepâ‚‚ _ id-comm-sym _ _ _
      (pulll[] _ (Ï€*.commutesp id-comm _)
        âˆ™[] pullr[] _ (Ï€*.commutes _ _))
      (pulll[] _ (Ï€*.commutes _ _)
        âˆ™[] Ï€*.commutesp id-comm _)
    âˆ™[] pushl[] _ (symP (Ï€*.commutesv _))
```
-->

```agda
module _ {ğ’¶} {ğ’·} {ğ’¸} (f : Hom ğ’· ğ’¸) (g : Hom ğ’¶ ğ’·) where
  private
    module FC = Cat.Reasoning (Cat[ Fibre E ğ’¸ , Fibre E ğ’¶ ])
    module Fa = Cat.Reasoning (Fibre E ğ’¶)

  base-change-comp : base-change (f âˆ˜ g) FC.â‰… (base-change g Fâˆ˜ base-change f)
```

<details>
<summary> This proof is a truly nightmarish application of universal
properties and I recommend that nobody look at it, ever. </summary>.

```agda
  base-change-comp = to-natural-iso mi where
    open make-natural-iso
    mi : make-natural-iso (base-change (f âˆ˜ g)) (base-change g Fâˆ˜ base-change f)
    mi .eta x = ^*-comp-to
    mi .inv x = ^*-comp-from
    mi .etaâˆ˜inv x = ^*-comp .Fib.invl
    mi .invâˆ˜eta x = ^*-comp .Fib.invr
    mi .natural x y f' = ^*-comp-to-natural f'
```
</details>

In order to assemble this into a [[pseudofunctor]] out of $\cB\op$
(seen as a [[locally discrete bicategory]]) into $\Cat$, we start by
bundling up all the base changes into a functor between $\hom$ categories.
We also prove a lemma that will be useful later, relating base changes
along equal morphisms.

```agda
base-changes : âˆ€ {a b}
  â†’ Functor (Locally-discrete (B ^op) .Prebicategory.Hom a b)
            Cat[ Fibre E a , Fibre E b ]
base-changes = Disc'-adjunct base-change

base-change-coherence
  : âˆ€ {a b} {b' : Ob[ b ]} {f g : Hom a b} (p : f â‰¡ g)
  â†’ Ï€* g b' âˆ˜' base-changes .Fâ‚ p .Î· b'
  â‰¡[ idr _ âˆ™ sym p ] Ï€* f b'
base-change-coherence {b' = b'} {f} = J
  (Î» g p â†’ Ï€* g b' âˆ˜' base-changes .Fâ‚ p .Î· b'
         â‰¡[ idr _ âˆ™ sym p ] Ï€* f b')
  (elimr' refl Regularity.reduce!)
```

We have enough data to start defining our pseudofunctor:

<!--
```agda
private
  module FC {a} {b} = Cat.Reasoning (Cat[ Fibre E a , Fibre E b ])
```
-->

```agda
Fibres : Pseudofunctor (Locally-discrete (B ^op)) (Cat o' â„“')
Fibres .lax .Pâ‚€ = Fibre E
Fibres .lax .Pâ‚ = base-changes
Fibres .lax .compositor = Disc-naturalâ‚‚
  Î» (f , g) â†’ base-change-comp g f .Mor.from
Fibres .lax .unitor = base-change-id .Mor.from
Fibres .unitor-inv = FC.isoâ†’invertible (base-change-id FC.Isoâ»Â¹)
Fibres .compositor-inv f g =
  FC.isoâ†’invertible (base-change-comp g f FC.Isoâ»Â¹)
```

It remains to verify that this data is *coherent*, which is so tedious
that it serves as a decent self-contained motivation for displayed
categories.

<details>
<summary>You've been warned.</summary>

We start with the `left-unit`{.Agda}. In the diagram below, we have
to show that the composite vertical morphism over $b$ is equal to
the identity over $b$. By the uniqueness property of cartesian lifts,
it suffices to show that the composites with the lift of $f$ are equal,
which is witnessed by the commutativity of the whole diagram.

~~~{.quiver}
\[\begin{tikzcd}
  {f^*a'} \\
  {\id^*f^*a'} & {f^*a'} \\
  {(f \circ \id)^*a'} \\
  {f^*a'} && {a'} \\
  b && a
  \arrow["f", from=5-1, to=5-3]
  \arrow["{\rm{lift}(f)}"', from=4-1, to=4-3]
  \arrow[maps to, from=4-3, to=5-3]
  \arrow[maps to, from=4-1, to=5-1]
  \arrow["{\lambda^*a'}"', color={rgb,255:red,214;green,92;blue,92}, from=3-1, to=4-1]
  \arrow["{\rm{lift}(f \circ \id)}"{pos=0.4}, from=3-1, to=4-3]
  \arrow["\gamma"', color={rgb,255:red,214;green,92;blue,92}, from=2-1, to=3-1]
  \arrow["\upsilon"', color={rgb,255:red,214;green,92;blue,92}, from=1-1, to=2-1]
  \arrow["{\rm{lift}(f)}", from=2-2, to=4-3]
  \arrow["{\rm{lift}(\id)}"', from=2-1, to=2-2]
  \arrow["\id", from=1-1, to=2-2]
\end{tikzcd}\]
~~~

The bottom triangle is our `base-change-coherence`{.Agda} lemma, the
middle square is by definition of the compositor and the top triangle
is by definition of the unitor.

```agda
Fibres .lax .left-unit f = ext Î» a' â†’
  Ï€*.uniquepâ‚‚ _ refl refl _ _
    (Fib.pulllf (base-change-coherence (idr f))
    âˆ™[] Fib.pulllf (Ï€*.commutesv _)
    âˆ™[] (reflâŸ©âˆ˜'âŸ¨ Fib.eliml (base-change id .F-id))
    âˆ™[] pullr[] _ (Ï€*.commutesv id'))
    refl
```

For the `right-unit`{.Agda}, we proceed similarly. The diagram below
shows that the composite on the left, composed with the lift of $f$,
is equal to the lift of $f$.

~~~{.quiver}
\[\begin{tikzcd}
  {f^*a'} && {a'} \\
  {f^*\id^*a'} && {\id^*a'} \\
  {(\id \circ f)^*a'} \\
  {f^*a'} && {a'} \\
  b && a
  \arrow["f", from=5-1, to=5-3]
  \arrow["{\rm{lift}(f)}"', from=4-1, to=4-3]
  \arrow[maps to, from=4-3, to=5-3]
  \arrow[maps to, from=4-1, to=5-1]
  \arrow["{\rho^*a'}"', color={rgb,255:red,214;green,92;blue,92}, from=3-1, to=4-1]
  \arrow["{\rm{lift}(\id \circ f)}"{pos=0.2}, from=3-1, to=4-3]
  \arrow["\gamma"', color={rgb,255:red,214;green,92;blue,92}, from=2-1, to=3-1]
  \arrow["{f^*\upsilon}"', color={rgb,255:red,214;green,92;blue,92}, from=1-1, to=2-1]
  \arrow["{\rm{lift}(\id)}"{description}, from=2-3, to=4-3]
  \arrow["{\rm{lift}(f)}"', from=2-1, to=2-3]
  \arrow["{\rm{lift}(f)}", from=1-1, to=1-3]
  \arrow["\upsilon"', from=1-3, to=2-3]
  \arrow["\id", curve={height=-30pt}, from=1-3, to=4-3]
\end{tikzcd}\]
~~~

The bottom triangle is `base-change-coherence`{.Agda}, the middle square
is by definition of the compositor, the outer triangle is by definition
of the unitor, and the top square is by definition of `rebase`{.Agda}
(the action of $f^*$ on morphisms).

```agda
Fibres .lax .right-unit f = ext Î» a' â†’
  Ï€*.uniquepâ‚‚ _ refl _ _ _
    (Fib.pulllf (base-change-coherence (idl f))
    âˆ™[] Fib.pulllf (Ï€*.commutesv _)
    âˆ™[] (reflâŸ©âˆ˜'âŸ¨ Fib.idr _)
    âˆ™[] extendr[] id-comm (Ï€*.commutesp _ _)
    âˆ™[] (Ï€*.commutesv id' âŸ©âˆ˜'âŸ¨refl))
    (idr' _ âˆ™[] symP (idl' _))
```

Last but definitely not least, the `hexagon`{.Agda} witnessing the
coherence of associativity follows again by uniqueness of cartesian
lifts, by the commutativity of the following diagram.

~~~{.quiver}
\[\begin{tikzcd}
  {f^*g^*h^*a'} &&&&&& {f^*g^*h^*a'} \\
  {f^*g^*h^*a'} & {g^*h^*a'} &&&& {g^*h^*a'} & {(gf)^*h^*a'} \\
  {f^*(hg)^*a'} & {(hg)^*a'} & {h^*a'} && {h^*a'} && {(h(gf))^*a'} \\
  {((hg)f)^*a'} &&& {a'} &&& {((hg)f)^*a'} \\
  d & c & b & a & b & c & d
  \arrow["f", from=5-1, to=5-2]
  \arrow["g", from=5-2, to=5-3]
  \arrow["h", from=5-3, to=5-4]
  \arrow[maps to, from=4-4, to=5-4]
  \arrow[maps to, from=4-1, to=5-1]
  \arrow["{\rm{lift}((hg)f)}"', from=4-1, to=4-4]
  \arrow[""{name=0, anchor=center, inner sep=0}, from=3-3, to=4-4]
  \arrow["{\rm{lift}(g)}", from=2-2, to=3-3]
  \arrow["{\rm{lift}(f)}", from=1-1, to=2-2]
  \arrow["\gamma", color={rgb,255:red,214;green,92;blue,92}, from=2-7, to=3-7]
  \arrow["\id"', color={rgb,255:red,92;green,92;blue,214}, from=1-1, to=2-1]
  \arrow["{f^*\gamma}"', color={rgb,255:red,92;green,92;blue,214}, from=2-1, to=3-1]
  \arrow["\gamma"', color={rgb,255:red,92;green,92;blue,214}, from=3-1, to=4-1]
  \arrow["{\rm{lift}(hg)}"'{pos=0.1}, from=3-2, to=4-4]
  \arrow["{\rm{lift}(f)}"', from=3-1, to=3-2]
  \arrow["{\rm{lift}(f)}"', from=2-1, to=2-2]
  \arrow["\gamma"', from=2-2, to=3-2]
  \arrow["{\alpha^*a'}", color={rgb,255:red,214;green,92;blue,92}, from=3-7, to=4-7]
  \arrow["\gamma", color={rgb,255:red,214;green,92;blue,92}, from=1-7, to=2-7]
  \arrow["h"', from=5-5, to=5-4]
  \arrow["g"', from=5-6, to=5-5]
  \arrow["f"', from=5-7, to=5-6]
  \arrow[maps to, from=4-7, to=5-7]
  \arrow["{\rm{lift}((hg)f)}", from=4-7, to=4-4]
  \arrow[""{name=1, anchor=center, inner sep=0}, from=3-5, to=4-4]
  \arrow["{\rm{lift}(g)}"', from=2-6, to=3-5]
  \arrow["{\rm{lift}(f)}"', from=1-7, to=2-6]
  \arrow["{\rm{lift}(h(gf))}"{pos=0.2}, from=3-7, to=4-4]
  \arrow["{\rm{lift}(gf)}"{pos=0.3}, from=2-7, to=3-5]
  \arrow[Rightarrow, no head, from=1-1, to=1-7]
  \arrow[Rightarrow, no head, from=2-2, to=2-6]
  \arrow[Rightarrow, no head, from=3-3, to=3-5]
  \arrow["{\rm{lift}(h)}"{description}, shift left=2, draw=none, from=0, to=1]
\end{tikzcd}\]
~~~

```agda
Fibres .lax .hexagon f g h = ext Î» a' â†’
  Ï€*.uniquepâ‚‚ _ refl _ _ _
    (Fib.pulllf (base-change-coherence (assoc h g f))
    âˆ™[] Fib.pulllf (Ï€*.commutesv _)
    âˆ™[] (reflâŸ©âˆ˜'âŸ¨ Fib.eliml (base-change (g âˆ˜ f) .F-id))
    âˆ™[] extendr[] _ (Ï€*.commutesv _))
    (Fib.pulllf (Ï€*.commutesv _)
    âˆ™[] (reflâŸ©âˆ˜'âŸ¨ Fib.idr _) âˆ™[] (reflâŸ©âˆ˜'âŸ¨ Fib.idr _)
    âˆ™[] extendr[] id-comm (Ï€*.commutesp _ _)
    âˆ™[] (Ï€*.commutesv _ âŸ©âˆ˜'âŸ¨refl))
```
</details>

<!--
```agda
-- Optimized natural iso, avoids a bunch of junk from composition.
opaque
  base-change-square
    : âˆ€ {Î“ Î” Î˜ Î¨ : Ob}
    â†’ {Ïƒ : Hom Î“ Î”} {Î´ : Hom Î“ Î˜} {Î³ : Hom Î” Î¨} {Ï„ : Hom Î˜ Î¨}
    â†’ Î³ âˆ˜ Ïƒ â‰¡ Ï„ âˆ˜ Î´
    â†’ âˆ€ x' â†’ Hom[ id ]
      (base-change Ïƒ .Fâ‚€ (base-change Î³ .Fâ‚€ x'))
      (base-change Î´ .Fâ‚€ (base-change Ï„ .Fâ‚€ x'))
  base-change-square {Ïƒ = Ïƒ} {Î´ = Î´} {Î³ = Î³} {Ï„ = Ï„} p x' =
    Ï€*.universalv $
    Ï€*.universal' (sym p) $
    Ï€* Î³ x' âˆ˜' Ï€* Ïƒ _

  base-change-square-lifting
    : âˆ€ {Î“ Î” Î˜ Î¨ : Ob}
    â†’ {Ïƒ : Hom Î“ Î”} {Î´ : Hom Î“ Î˜} {Î³ : Hom Î” Î¨} {Ï„ : Hom Î˜ Î¨}
    â†’ (p : Î³ âˆ˜ Ïƒ â‰¡ Ï„ âˆ˜ Î´) (x' : Ob[ Î¨ ])
    â†’ Ï€* Ï„ x' âˆ˜' Ï€* Î´ (base-change Ï„ .Fâ‚€ x') âˆ˜' base-change-square p x'
    â‰¡[ ap (Ï„ âˆ˜_) (idr _) âˆ™ sym p ] Ï€* Î³ x' âˆ˜' Ï€* Ïƒ _
  base-change-square-lifting {Ïƒ = Ïƒ} {Î´ = Î´} {Î³ = Î³} {Ï„ = Ï„} p x' =
    cast[] $
    apd (Î» _ â†’ Ï€* Ï„ x' âˆ˜'_) (Ï€*.commutesv _)
    âˆ™[] Ï€*.commutesp (sym p) _

  base-change-square-natural
    : âˆ€ {Î“ Î” Î˜ Î¨ : Ob}
    â†’ {Ïƒ : Hom Î“ Î”} {Î´ : Hom Î“ Î˜} {Î³ : Hom Î” Î¨} {Ï„ : Hom Î˜ Î¨}
    â†’ (p : Î³ âˆ˜ Ïƒ â‰¡ Ï„ âˆ˜ Î´)
    â†’ âˆ€ {x' y'} (f' : Hom[ id ] x' y')
    â†’ base-change-square p y' âˆ˜' base-change Ïƒ .Fâ‚ (base-change Î³ .Fâ‚ f')
    â‰¡ base-change Î´ .Fâ‚ (base-change Ï„ .Fâ‚ f') âˆ˜' base-change-square p x'
  base-change-square-natural {Ïƒ = Ïƒ} {Î´ = Î´} {Î³ = Î³} {Ï„ = Ï„} p f' =
    Ï€*.uniquepâ‚‚ _ _ _ _ _
      (pulll[] _ (Ï€*.commutesv _)
       âˆ™[] Ï€*.uniquepâ‚‚ _ (idr _) _ _ _
         (pulll[] _ (Ï€*.commutesp (sym p) _)
          âˆ™[] pullr[] _ (Ï€*.commutesp id-comm _)
          âˆ™[] extendl[] _ (Ï€*.commutesp id-comm _))
         (Ï€*.commutesp (sym p âˆ™ sym (idl _ )) _))
      (pulll[] _ (Ï€*.commutesp id-comm _)
       âˆ™[] pullr[] _ (Ï€*.commutesv _)
       âˆ™[] Ï€*.uniquep _ (idl _) (sym p âˆ™ sym (idl _)) _
         (pulll[] _ (Ï€*.commutesp id-comm _ )
          âˆ™[] pullr[] _ (Ï€*.commutesp (sym p) _)))

  base-change-square-inv
    : âˆ€ {Î“ Î” Î˜ Î¨ : Ob}
    â†’ {Ïƒ : Hom Î“ Î”} {Î´ : Hom Î“ Î˜} {Î³ : Hom Î” Î¨} {Ï„ : Hom Î˜ Î¨}
    â†’ (p : Î³ âˆ˜ Ïƒ â‰¡ Ï„ âˆ˜ Î´)
    â†’ âˆ€ x' â†’ base-change-square p x' âˆ˜' base-change-square (sym p) x' â‰¡[ idl _ ] id'
  base-change-square-inv {Ïƒ = Ïƒ} {Î´ = Î´} {Î³ = Î³} {Ï„ = Ï„} p x' =
    Ï€*.uniquepâ‚‚ _ _ _ _ _
      (pulll[] _ (Ï€*.commutesv _)
       âˆ™[] Ï€*.uniquepâ‚‚ _ (idr _) refl _ _
         (pulll[] _ (Ï€*.commutesp (sym p) _)
          âˆ™[] pullr[] _ (Ï€*.commutesv _)
          âˆ™[] Ï€*.commutesp p _)
         refl)
      (idr' _)

base-change-square-ni
  : âˆ€ {Î“ Î” Î˜ Î¨ : Ob}
  â†’ {Ïƒ : Hom Î“ Î”} {Î´ : Hom Î“ Î˜} {Î³ : Hom Î” Î¨} {Ï„ : Hom Î˜ Î¨}
  â†’ Î³ âˆ˜ Ïƒ â‰¡ Ï„ âˆ˜ Î´
  â†’ (base-change Ïƒ Fâˆ˜ base-change Î³) â‰…â¿ (base-change Î´ Fâˆ˜ base-change Ï„)
base-change-square-ni {Ïƒ = Ïƒ} {Î´ = Î´} {Î³ = Î³} {Ï„ = Ï„} p =
  to-natural-iso ni where

  open make-natural-iso
  ni : make-natural-iso _ _
  ni .eta = base-change-square p
  ni .inv = base-change-square (sym p)
  ni .etaâˆ˜inv x = from-pathp[] $ base-change-square-inv p x
  ni .invâˆ˜eta x = from-pathp[] $ base-change-square-inv (sym p) x
  ni .natural x y f = sym $ Fib.over-fibre (base-change-square-natural p f)
```
-->
