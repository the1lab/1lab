---
description: |
  We define right fibrations, and characterize their fibre categories.
---
```agda
open import Cat.Displayed.Base
open import Cat.Displayed.Cartesian.Discrete
open import Cat.Displayed.Functor
open import Cat.Prelude

import Cat.Reasoning
import Cat.Displayed.Cartesian
import Cat.Displayed.Morphism
import Cat.Displayed.Reasoning

module Cat.Displayed.Cartesian.Right
  {o â„“ oâ€² â„“â€²}
  {â„¬ : Precategory o â„“}
  (â„° : Displayed â„¬ oâ€² â„“â€²)
  where

open Cat.Reasoning â„¬
open Displayed â„°
open Cat.Displayed.Cartesian â„°
open Cat.Displayed.Morphism â„°
open Cat.Displayed.Reasoning â„°
```

# Right Fibrations

A [cartesian fibration] $\cE$ is said to be a **right fibration** if every
morphism in $\cE$ is cartesian.

```agda
record Right-fibration : Type (o âŠ” â„“ âŠ” oâ€² âŠ” â„“â€²) where
  no-eta-equality
  field
    is-fibration : Cartesian-fibration
    cartesian
      : âˆ€ {x y} {f : Hom x y}
      â†’ âˆ€ {xâ€² yâ€²} (fâ€² : Hom[ f ] xâ€² yâ€²)
      â†’ is-cartesian f fâ€²

  open Cartesian-fibration is-fibration public
```

One notable fact is every vertical morphism of a right fibrations is
invertible. In other words, if $\cE$ is a right fibration, then all
of its fibre categories are groupoids. This follows directly from the
fact that vertical cartesian maps are invertible.

```agda
right-fibrationâ†’vertical-invertible
  : Right-fibration
  â†’ âˆ€ {x} {xâ€² xâ€³ : Ob[ x ]} â†’ (fâ€² : Hom[ id ] xâ€² xâ€³) â†’ is-invertibleâ†“ fâ€²
right-fibrationâ†’vertical-invertible rfib fâ€² =
  vertical+cartesianâ†’invertible (Right-fibration.cartesian rfib fâ€²)
```

More notably, this is an exact characterization of categories fibred
in groupoids! If $\cE$ is a cartesian fibration where all vertical
morphisms are invertible, then it must be a right fibration.

```agda
vertical-invertible+fibrationâ†’right-fibration
  : Cartesian-fibration
  â†’ (âˆ€ {x} {xâ€² xâ€³ : Ob[ x ]} â†’ (fâ€² : Hom[ id ] xâ€² xâ€³) â†’ is-invertibleâ†“ fâ€²)
  â†’ Right-fibration
vertical-invertible+fibrationâ†’right-fibration fib vert-inv
  .Right-fibration.is-fibration = fib
vertical-invertible+fibrationâ†’right-fibration fib vert-inv
  .Right-fibration.cartesian {x = x} {f = f} {xâ€² = xâ€²} {yâ€² = yâ€²} fâ€² = f-cart where
    open Cartesian-fibration fib
    open Cartesian-lift renaming (xâ€² to x-lift)
```

To see this, recall that cartesian morphisms are [stable under
vertical retractions]. The cartesian lift $f^{*}$ of $f$ is obviously
cartesian, so it suffices to show that there is a vertical retraction
$x^{*} \to x'$. To construct this retraction, we shall factorize $f'$
over $f \cdot id$; which yields a vertical morphism $i^{*} : x' \to x^{*}$.
By our hypotheses, $i^{*}$ is invertible, and thus is a retraction.
What remains to be shown is that the inverse to $i^{*}$ factors
$f'$ and $f^{*}$; this follows from the factorization of $f'$ and
the fact that $i^{*}$ is invertible.

[stable under vertical retractions]: Cat.Displayed.Cartesian.html#cartesian-vertical-retraction-stable

```agda
    x* : Ob[ x ]
    x* = has-lift f yâ€² .x-lift

    f* : Hom[ f ] x* yâ€²
    f* = has-lift f yâ€² .lifting

    module f* = is-cartesian (has-lift f yâ€² .cartesian)

    i* : Hom[ id ] xâ€² x*
    i* = f*.universalâ€² (idr f) fâ€²

    module i*-inv = is-invertible[_] (vert-inv i*)

    i*â»Â¹ : Hom[ id ] x* xâ€²
    i*â»Â¹ = i*-inv.invâ€²

    factors : fâ€² âˆ˜â€² i*â»Â¹ â‰¡[ idr f ] f*
    factors = to-pathpâ» $
      fâ€² âˆ˜â€² i*â»Â¹               â‰¡âŸ¨ shiftr _ (pushlâ€² _ (symP $ f*.commutesp (idr f) fâ€²) {q = ap (f âˆ˜_) (sym (idl _))}) âŸ©
      hom[] (f* âˆ˜â€² i* âˆ˜â€² i*â»Â¹) â‰¡âŸ¨ weave _ (elimr (idl id)) _ (elimrâ€² _ i*-inv.invlâ€²) âŸ©
      hom[] f* âˆ

    f-cart : is-cartesian f fâ€²
    f-cart = cartesian-vertical-retraction-stable
      (has-lift f yâ€² .cartesian)
      (inverses[]â†’from-has-section[] i*-inv.inversesâ€²)
      factors
```

As a corollary, we get that all discrete fibrations are right fibrations.
Intuitively, this is true, as sets are 0-groupoids.

```agda
discreteâ†’right-fibration
  : Discrete-fibration â„°
  â†’ Right-fibration
discreteâ†’right-fibration dfib =
  vertical-invertible+fibrationâ†’right-fibration
    (discreteâ†’cartesian â„° dfib)
    (discreteâ†’vertical-invertible â„° dfib)
```

## Fibred Functors and Right Fibrations

As every map in a right fibration is cartesian, every displayed functor
into a right fibration is automatically fibred.

```agda
functor+right-fibrationâ†’fibred
  : âˆ€ {oâ‚‚ â„“â‚‚ oâ‚‚â€² â„“â‚‚â€²}
  â†’ {ğ’Ÿ : Precategory oâ‚‚ â„“â‚‚}
  â†’ {â„± : Displayed ğ’Ÿ oâ‚‚â€² â„“â‚‚â€²}
  â†’ {F : Functor ğ’Ÿ â„¬}
  â†’ Right-fibration
  â†’ (Fâ€² : Displayed-functor â„± â„° F)
  â†’ Fibred-functor â„± â„° F
functor+right-fibrationâ†’fibred rfib Fâ€² .Fibred-functor.disp =
  Fâ€²
functor+right-fibrationâ†’fibred rfib Fâ€² .Fibred-functor.F-cartesian fâ€² _ =
  Right-fibration.cartesian rfib (Fâ‚â€² fâ€²)
  where open Displayed-functor Fâ€²
```

Specifically, this implies that all displayed functors into a discrete
fibrations are fibred. This means that we can drop the fibred condition
when working with functors between discrete fibrations.

```agda
functor+discreteâ†’fibred
  : âˆ€ {oâ‚‚ â„“â‚‚ oâ‚‚â€² â„“â‚‚â€²}
  â†’ {ğ’Ÿ : Precategory oâ‚‚ â„“â‚‚}
  â†’ {â„± : Displayed ğ’Ÿ oâ‚‚â€² â„“â‚‚â€²}
  â†’ {F : Functor ğ’Ÿ â„¬}
  â†’ Discrete-fibration â„°
  â†’ (Fâ€² : Displayed-functor â„± â„° F)
  â†’ Fibred-functor â„± â„° F
functor+discreteâ†’fibred disc Fâ€² =
  functor+right-fibrationâ†’fibred (discreteâ†’right-fibration disc) Fâ€²
```
