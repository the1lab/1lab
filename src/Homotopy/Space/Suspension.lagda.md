<!--
```agda
open import 1Lab.Reflection.Induction
open import 1Lab.Prelude
```
-->

```agda
module Homotopy.Space.Suspension where
```

<!--
```agda
private variable
  ℓ : Level
  A B C : Type ℓ
  A∙ B∙ C∙ : Type∙ ℓ
```
-->

# Suspension {defines="suspension"}

Given a type $A$, its **(reduced) suspension** $\Susp A$ is the
higher-inductive type generated by the following constructors:

```agda
data Susp {ℓ} (A : Type ℓ) : Type ℓ where
  north : Susp A
  south : Susp A
  merid : A → north ≡ south
```

The names `north`{.Agda} and `south`{.Agda} are meant to evoke the
*n*orth and *s*outh poles of a sphere, respectively, and the name
`merid`{.Agda} should evoke the *merid*ians. Indeed, we can picture a
suspension like a sphere[^diamond]:

[^diamond]: Diagrams are hard, okay?!

~~~{.quiver}
\begin{tikzpicture}
  \node[draw,circle,fill,minimum width=1.2pt,inner sep=0,outer sep=2pt,label={[right,yshift=-2.6pt]\tiny N}] (N) at (0,+1.25) {};
  \node[draw,circle,fill,minimum width=1.2pt,inner sep=0,outer sep=2pt,label={[right,yshift=-2.6pt]\tiny S}] (S) at (0,-1.25) {};

  \coordinate (L) at (-0.8,0);
  \coordinate (R) at (+0.8,0);
  \coordinate (T) at (+0.25,+0.525);
  \coordinate (B) at (-0.25,-0.525);

  \filldraw[fill=diagramshaded] (T) -- (R) -- (B) -- (L) -- (T);

  \draw (N) -- (L);
  \draw (N) -- (R);
  \draw (N) -- (B);
  \draw[dashed] (N) -- (T);

  \draw (S) -- (L);
  \draw (S) -- (R);
  \draw (S) -- (B);

  \node[color=diagramshadedtext] (A) at (0,0) {\tiny $A$};
\end{tikzpicture}
~~~

We have the north and south poles $N$ and $S$ above and below a copy of
the space $A$, which is diagrammatically represented by the <span
class=shaded>shaded</span> region. In the type theory, we can't really
"see" this copy of $A$: we only see its _ghost_, as something keeping
all the meridians from collapsing.

By convention, we see the suspension as a [[pointed type]] with the
*north* pole as the base point.

```agda
Σ∙ : ∀ {ℓ} (A : Type ℓ) → Type∙ ℓ
Σ∙ A = Susp A , north

Σ¹ : ∀ {ℓ} → Type∙ ℓ → Type∙ ℓ
Σ¹ (A , _) = Σ∙ A
```

```agda
Susp-elim
  : ∀ {ℓ ℓ'} {A : Type ℓ} (P : Susp A → Type ℓ')
  → (pN : P north) (pS : P south)
  → (∀ x → PathP (λ i → P (merid x i)) pN pS)
  → ∀ x → P x
Susp-elim P pN pS pmerid north = pN
Susp-elim P pN pS pmerid south = pS
Susp-elim P pN pS pmerid (merid x i) = pmerid x i

unquoteDecl Susp-elim-prop = make-elim-n 1 Susp-elim-prop (quote Susp)
```

Every suspension admits a surjection from the booleans:

```agda
2→Σ : ∀ {ℓ} {A : Type ℓ} → Bool → Susp A
2→Σ true  = north
2→Σ false = south

2→Σ-surjective : is-surjective (2→Σ {A = A})
2→Σ-surjective = Susp-elim-prop (λ _ → hlevel 1)
  (inc (true , refl)) (inc (false , refl))
```

Suspension extends to a functor in the evident way.

```agda
Susp-map : (A → B) → Susp A → Susp B
Susp-map f north = north
Susp-map f south = south
Susp-map f (merid x i) = merid (f x) i

Susp-map∙ : (A∙ →∙ B∙) → Σ¹ A∙ →∙ Σ¹ B∙
Susp-map∙ (f , pt) = Susp-map f , refl

Susp-map∙-id : Susp-map∙ {A∙ = A∙} id∙ ≡ id∙
Susp-map∙-id = funext∙ (Susp-elim _ refl refl λ x i j → merid x i) refl

Susp-map∙-∘
  : (f : B∙ →∙ C∙) (g : A∙ →∙ B∙)
  → Susp-map∙ (f ∘∙ g) ≡ Susp-map∙ f ∘∙ Susp-map∙ g
Susp-map∙-∘ (f , _) (g , _) =
  funext∙ (Susp-elim _ refl refl (λ x i j → merid (f (g x)) i)) (sym (∙-idl _))

Susp-map∙-zero : Susp-map∙ (zero∙ {A = A∙} {B = B∙}) ≡ zero∙
Susp-map∙-zero {B∙ = B , b₀} =
  funext∙ (Susp-elim _ refl (sym (merid b₀)) λ a i j → merid b₀ (i ∧ ~ j)) refl
```

<!--
```agda
Susp-Maps∙ : Maps∙ A∙ B∙ →∙ Maps∙ (Σ¹ A∙) (Σ¹ B∙)
Susp-Maps∙ .fst = Susp-map∙
Susp-Maps∙ {A∙ = A∙} {B∙ = B∙} .snd = Susp-map∙-zero {A∙ = A∙} {B∙ = B∙}

Susp-ap : A ≃ B → Susp A ≃ Susp B
Susp-ap e .fst = Susp-map (e .fst)
Susp-ap e .snd = is-iso→is-equiv λ where
  .is-iso.from → Susp-elim _ north south (λ x → merid (Equiv.from e x))
  .is-iso.rinv → Susp-elim _ refl refl (λ x i j → merid (Equiv.ε e x j) i)
  .is-iso.linv → Susp-elim _ refl refl (λ x i j → merid (Equiv.η e x j) i)

Susp-ap∙ : A∙ ≃∙ B∙ → Σ¹ A∙ ≃∙ Σ¹ B∙
Susp-ap∙ (e , pt) .fst = Susp-ap e
Susp-ap∙ (e , pt) .snd = refl
```
-->
